open import Types
open import Labels
open import Data.Nat
open import Data.Product using (_Ã—_; projâ‚; projâ‚‚; Î£; Î£-syntax) renaming (_,_ to âŸ¨_,_âŸ©)
open import Data.Sum using (_âŠ_; injâ‚; injâ‚‚)
open import Data.Bool
open import Variables
open import Relation.Nullary using (Â¬_)
open import Relation.Nullary.Negation using (contradiction)
open import Relation.Binary.PropositionalEquality using (_â‰¡_;_â‰¢_; refl; trans; sym; cong; congâ‚‚; cong-app)
open import Data.Empty using (âŠ¥; âŠ¥-elim)

{-

  This modules defines reduction for the Parameterized Cast Calculus
  and provides a proof of progress. Preservation is guaranteed in the
  way the reduction relation is defined and checked by Agda.

-}


module ParamCastReduction
  (Cast : Type â†’ Set)
  (Inert : âˆ€{A} â†’ Cast A â†’ Set)
  (Active : âˆ€{A} â†’ Cast A â†’ Set)  
  (ActiveOrInert : âˆ€{A} â†’ (c : Cast A) â†’ Active c âŠ Inert c)
  where

  import ParamCastCalculus
  module CastCalc = ParamCastCalculus Cast
  open CastCalc

  {-

  Before defining reduction, we first need to define Value.  In cast
  calculi, whether a cast forms a value or not depends on the shape of
  the cast. But here we have parameterized over casts.  So we must add
  more parameters to tell us whether a cast is a value-forming cast or
  not. So we add the parameter Inert to identify the later, and the
  parameter Active to identify casts that need to be reduced. Further,
  we require that all casts (at least, all the well-typed ones) can be
  categorized one of these two ways, which is given by the
  ActiveOrInert parameter.

  The following is the definition of Value. The case for casts, M âŸ¨ c âŸ©,
  requires M to be a value and c to be an inert cast.

  -}
  data Value : âˆ€ {Î“ A} â†’ Î“ âŠ¢ A â†’ Set where

    V-Æ› : âˆ€ {Î“ A B} {N : Î“ , A âŠ¢ B}
        -----------
      â†’ Value (Æ› N)

    V-const : âˆ€ {Î“} {A : Type} {k : rep A} {f : Prim A}
        ------------------------
      â†’ Value {Î“} {A} (($ k){f})

    V-pair : âˆ€ {Î“ A B} {V : Î“ âŠ¢ A} {W : Î“ âŠ¢ B}
      â†’ Value V â†’ Value W
        -----------------
      â†’ Value (cons V W)

    V-inl : âˆ€ {Î“ A B} {V : Î“ âŠ¢ A}
      â†’ Value V
        --------------------------
      â†’ Value {Î“} {A `âŠ B} (inl V)

    V-inr : âˆ€ {Î“ A B} {V : Î“ âŠ¢ B}
      â†’ Value V
        --------------------------
      â†’ Value {Î“} {A `âŠ B} (inr V)

    V-cast : âˆ€ {Î“ : Context} {A B : Type} {V : Î“ âŠ¢ A} {c : Cast (A â‡’ B)}
        {i : Inert c}
      â†’ Value V
        ---------------
      â†’ Value (V âŸ¨ c âŸ©)


  {-

  A value of type â‹† must be of the form M âŸ¨ c âŸ© where c is inert cast.

  -}
  canonicalâ‹† : âˆ€ {Î“} â†’ (M : Î“ âŠ¢ â‹†) â†’ (Value M)
             â†’ Î£[ A âˆˆ Type ] Î£[ M' âˆˆ (Î“ âŠ¢ A) ] Î£[ c âˆˆ (Cast (A â‡’ â‹†)) ]
               Inert c Ã— (M â‰¡ (M' âŸ¨ c âŸ©))
  canonicalâ‹† .($ _) (V-const {k = ()})  
  canonicalâ‹† .(_ âŸ¨ _ âŸ©) (V-cast{Î“}{A}{B}{V}{c}{i} v) =
      âŸ¨ A , âŸ¨ V , âŸ¨ c , âŸ¨ i , refl âŸ© âŸ© âŸ© âŸ©

  {-

   We shall use a kind of shallow evaluation context, called a Frame,
   to collapse all of the Î¾ rules into a single rule.

  -}

  data Frame : {Î“ : Context} â†’ Type â†’ Type â†’ Set where

    F-Â·â‚ : âˆ€ {Î“ A B}
      â†’ Î“ âŠ¢ A
      â†’ Frame {Î“} (A â‡’ B) B

    F-Â·â‚‚ : âˆ€ {Î“ A B}
      â†’ (M : Î“ âŠ¢ A â‡’ B) â†’ âˆ€{v : Value {Î“} M}
      â†’ Frame {Î“} A B

    F-if : âˆ€ {Î“ A}
      â†’ Î“ âŠ¢ A
      â†’ Î“ âŠ¢ A    
      â†’ Frame {Î“} (` ğ”¹) A

    F-Ã—â‚ : âˆ€ {Î“ A B}
      â†’ Î“ âŠ¢ A
      â†’ Frame {Î“} B (A `Ã— B)

    F-Ã—â‚‚ : âˆ€ {Î“ A B}
      â†’ Î“ âŠ¢ B
      â†’ Frame {Î“} A (A `Ã— B)

    F-fst : âˆ€ {Î“ A B}
      â†’ Frame {Î“} (A `Ã— B) A

    F-snd : âˆ€ {Î“ A B}
      â†’ Frame {Î“} (A `Ã— B) B

    F-inl : âˆ€ {Î“ A B}
      â†’ Frame {Î“} A (A `âŠ B)

    F-inr : âˆ€ {Î“ A B}
      â†’ Frame {Î“} B (A `âŠ B)

    F-case : âˆ€ {Î“ A B C}
      â†’ Î“ âŠ¢ A â‡’ C
      â†’ Î“ âŠ¢ B â‡’ C
      â†’ Frame {Î“} (A `âŠ B) C

    F-cast : âˆ€ {Î“ A B}
      â†’ Cast (A â‡’ B)
      â†’ Frame {Î“} A B

  {-

  The plug function inserts an expression into the hole of a frame.

  -}

  plug : âˆ€{Î“ A B} â†’ Î“ âŠ¢ A â†’ Frame {Î“} A B â†’ Î“ âŠ¢ B
  plug L (F-Â·â‚ M)      = L Â· M
  plug M (F-Â·â‚‚ L)      = L Â· M
  plug L (F-if M N)    = if L M N
  plug L (F-Ã—â‚ M)      = cons M L
  plug M (F-Ã—â‚‚ L)      = cons M L
  plug M (F-fst)      = fst M
  plug M (F-snd)      = snd M
  plug M (F-inl)      = inl M
  plug M (F-inr)      = inr M
  plug L (F-case M N) = case L M N
  plug M (F-cast c) = M âŸ¨ c âŸ©

  {-

  We need a few more parameters to define reduction in a generic way.
  In particular, we need parameters that say how to reduce casts and
  how to eliminate values wrapped in casts. 
  * The applyCast parameter, applies an Active cast to a value. 
  * The funCast parameter applies a function wrapped in an inert cast
    to an argument.
  * The fstCast and sndCast parameters take the first or second part
    of a pair wrapped in an inert cast.
  * The caseCast performs a case-elimination on a value of sum type (inl or inr)
    that is wrapped in an inert cast.
  * The baseNotInert parameter ensures that every cast to a base type
    is not inert.

  We define a nested module named Reduction with these parameters
  because they all depend on parameters of the outer module, and it
  seems that Agda does not allow parameters to depend on other
  parameters of the same module.

  -}

  module Reduction
    (applyCast : âˆ€{Î“ A B} â†’ (M : Î“ âŠ¢ A) â†’ Value M â†’ (c : Cast (A â‡’ B))
                 â†’ âˆ€ {a : Active c} â†’ Î“ âŠ¢ B)
    (funSrc : âˆ€{A A' B'}
            â†’ (c : Cast (A â‡’ (A' â‡’ B'))) â†’ (i : Inert c)
            â†’ Î£[ Aâ‚ âˆˆ Type ] Î£[ Aâ‚‚ âˆˆ Type ] A â‰¡ Aâ‚ â‡’ Aâ‚‚)
    (pairSrc : âˆ€{A A' B'}
            â†’ (c : Cast (A â‡’ (A' `Ã— B'))) â†’ (i : Inert c)
            â†’ Î£[ Aâ‚ âˆˆ Type ] Î£[ Aâ‚‚ âˆˆ Type ] A â‰¡ Aâ‚ `Ã— Aâ‚‚)
    (sumSrc : âˆ€{A A' B'}
            â†’ (c : Cast (A â‡’ (A' `âŠ B'))) â†’ (i : Inert c)
            â†’ Î£[ Aâ‚ âˆˆ Type ] Î£[ Aâ‚‚ âˆˆ Type ] A â‰¡ Aâ‚ `âŠ Aâ‚‚)
    (dom : âˆ€{Aâ‚ Aâ‚‚ A' B'} â†’ (c : Cast ((Aâ‚ â‡’ Aâ‚‚) â‡’ (A' â‡’ B'))) â†’ Inert c
         â†’ Cast (A' â‡’ Aâ‚))
    (cod : âˆ€{Aâ‚ Aâ‚‚ A' B'} â†’ (c : Cast ((Aâ‚ â‡’ Aâ‚‚) â‡’ (A' â‡’ B'))) â†’ Inert c
         â†’  Cast (Aâ‚‚ â‡’ B'))
    (fstC : âˆ€{Aâ‚ Aâ‚‚ Bâ‚ Bâ‚‚} â†’ (c : Cast ((Aâ‚ `Ã— Aâ‚‚) â‡’ (Bâ‚ `Ã— Bâ‚‚))) â†’ Inert c
         â†’ Cast (Aâ‚ â‡’ Bâ‚))
    (sndC : âˆ€{Aâ‚ Aâ‚‚ Bâ‚ Bâ‚‚} â†’ (c : Cast ((Aâ‚ `Ã— Aâ‚‚) â‡’ (Bâ‚ `Ã— Bâ‚‚))) â†’ Inert c
         â†’  Cast (Aâ‚‚ â‡’ Bâ‚‚))
    (inlC : âˆ€{Aâ‚ Aâ‚‚ Bâ‚ Bâ‚‚} â†’ (c : Cast ((Aâ‚ `âŠ Aâ‚‚) â‡’ (Bâ‚ `âŠ Bâ‚‚))) â†’ Inert c
         â†’ Cast (Aâ‚ â‡’ Bâ‚))
    (inrC : âˆ€{Aâ‚ Aâ‚‚ Bâ‚ Bâ‚‚} â†’ (c : Cast ((Aâ‚ `âŠ Aâ‚‚) â‡’ (Bâ‚ `âŠ Bâ‚‚))) â†’ Inert c
         â†’  Cast (Aâ‚‚ â‡’ Bâ‚‚))
    (baseNotInert : âˆ€ {A Î¹} â†’ (c : Cast (A â‡’ ` Î¹)) â†’ Â¬ Inert c)
    where
    {- to do : add condition A â‰¢ â‹† to baseNotInert -}

    {-

      The following defines the reduction relation for the
      Parameterized Cast Calulus.  The reductions involving casts
      simply dispatch to the appropriate parameters of this
      module. This includes the cast, fun-cast, fst-cast, snd-cast,
      and case-cast rules. To propagate blame to the top of the
      program, we have the Î¾-blame rule. All of the usual congruence
      rules are instances of the one Î¾ rule with the appropriate
      choice of frame. The remaining rules are the usual Î² and Î´
      reduction rules of the STLC.

      The reduction relation has a very specific type signature,
      mapping only well-typed terms to well-typed terms, so
      preservation is guaranteed by construction.

     -}

    infix 2 _â€”â†’_
    data _â€”â†’_ : âˆ€ {Î“ A} â†’ (Î“ âŠ¢ A) â†’ (Î“ âŠ¢ A) â†’ Set where

      Î¾ : âˆ€ {Î“ A B} {M Mâ€² : Î“ âŠ¢ A} {F : Frame A B}
        â†’ M â€”â†’ Mâ€²
          ---------------------
        â†’ plug M F â€”â†’ plug Mâ€² F

      Î¾-blame : âˆ€ {Î“ A B} {F : Frame {Î“} A B} {â„“}
          ---------------------------
        â†’ plug (blame â„“) F â€”â†’ blame â„“

      Î² : âˆ€ {Î“ A B} {N : Î“ , A âŠ¢ B} {W : Î“ âŠ¢ A}
        â†’ Value W
          --------------------
        â†’ (Æ› N) Â· W â€”â†’ N [ W ]

      Î´ : âˆ€ {Î“ : Context} {A B} {f : rep A â†’ rep B} {k : rep A} {ab} {a} {b}
          ---------------------------------------------------
        â†’ ($_ {Î“}{A â‡’ B} f {ab}) Â· (($ k){a}) â€”â†’ ($ (f k)){b}

      Î²-if-true :  âˆ€ {Î“ A} {M : Î“ âŠ¢ A} {N : Î“ âŠ¢ A}{f}
          -------------------------
        â†’ if (($ true){f}) M N â€”â†’ M

      Î²-if-false :  âˆ€ {Î“ A} {M : Î“ âŠ¢ A} {N : Î“ âŠ¢ A}{f}
          --------------------------
        â†’ if (($ false){f}) M N â€”â†’ N

      Î²-fst :  âˆ€ {Î“ A B} {V : Î“ âŠ¢ A} {W : Î“ âŠ¢ B}
        â†’ Value V â†’ Value W
          --------------------
        â†’ fst (cons V W) â€”â†’ V

      Î²-snd :  âˆ€ {Î“ A B} {V : Î“ âŠ¢ A} {W : Î“ âŠ¢ B}
        â†’ Value V â†’ Value W
          --------------------
        â†’ snd (cons V W) â€”â†’ W

      Î²-caseL : âˆ€ {Î“ A B C} {V : Î“ âŠ¢ A} {L : Î“ âŠ¢ A â‡’ C} {M : Î“ âŠ¢ B â‡’ C}
        â†’ Value V
          --------------------------
        â†’ case (inl V) L M â€”â†’ L Â· V

      Î²-caseR : âˆ€ {Î“ A B C} {V : Î“ âŠ¢ B} {L : Î“ âŠ¢ A â‡’ C} {M : Î“ âŠ¢ B â‡’ C}
        â†’ Value V
          --------------------------
        â†’ case (inr V) L M â€”â†’ M Â· V

      cast : âˆ€ {Î“ A B} {V : Î“ âŠ¢ A} {c : Cast (A â‡’ B)}
        â†’ (v : Value V) â†’ {a : Active c}
          ------------------------------
        â†’ V âŸ¨ c âŸ© â€”â†’ applyCast V v c {a}

      fun-cast : âˆ€ {Î“ A' B' Aâ‚ Aâ‚‚} {V : Î“ âŠ¢ Aâ‚ â‡’ Aâ‚‚} {W : Î“ âŠ¢ A'}
          {c : Cast ((Aâ‚ â‡’ Aâ‚‚) â‡’ (A' â‡’ B'))}
        â†’ (v : Value V) â†’ Value W â†’ {i : Inert c}
          --------------------------------------------------
        â†’ (V âŸ¨ c âŸ©) Â· W â€”â†’ (V Â· (W âŸ¨ dom c i âŸ©)) âŸ¨ cod c i âŸ©

      fst-cast : âˆ€ {Î“ A B A' B'} {V : Î“ âŠ¢ A `Ã— B}
          {c : Cast ((A `Ã— B) â‡’ (A' `Ã— B'))}
        â†’ Value V â†’ {i : Inert c}
          -------------------------------------
        â†’ fst (V âŸ¨ c âŸ©) â€”â†’ (fst V) âŸ¨ fstC c i âŸ©

      snd-cast : âˆ€ {Î“ A B A' B'} {V : Î“ âŠ¢ A `Ã— B}
          {c : Cast ((A `Ã— B) â‡’ (A' `Ã— B'))}
        â†’ Value V â†’ {i : Inert c}
          -------------------------------------
        â†’ snd (V âŸ¨ c âŸ©) â€”â†’ (snd V) âŸ¨ sndC c i âŸ©

      case-cast : âˆ€ {Î“ A B A' B' C} {V : Î“ âŠ¢ A `âŠ B}
          {Wâ‚ : Î“ âŠ¢ A' â‡’ C } {Wâ‚‚ : Î“ âŠ¢ B' â‡’ C}
          {c : Cast ((A `âŠ B) â‡’ (A' `âŠ B'))}
        â†’ Value V â†’ {i : Inert c}
          --------------------------------------------
        â†’ case (V âŸ¨ c âŸ©) Wâ‚ Wâ‚‚ â€”â†’
          case V (Æ› ((rename S_ Wâ‚) Â· ((` Z) âŸ¨ inlC c i âŸ© )))
                 (Æ› ((rename S_ Wâ‚‚) Â· ((` Z) âŸ¨ inrC c i âŸ© )))

    infix  2 _â€”â† _
    infixr 2 _â€”â†’âŸ¨_âŸ©_
    infix  3 _âˆ

    data _â€”â† _ : âˆ€{Î“}{A} â†’ Î“ âŠ¢ A â†’ Î“ âŠ¢ A â†’ Set where
      _âˆ : âˆ€ {Î“}{A} (M : Î“ âŠ¢ A)
          ---------
        â†’ M â€”â†  M

      _â€”â†’âŸ¨_âŸ©_ : âˆ€ {Î“}{A} (L : Î“ âŠ¢ A) {M N : Î“ âŠ¢ A}
        â†’ L â€”â†’ M
        â†’ M â€”â†  N
          ---------
        â†’ L â€”â†  N

    data Observe : Set where
      O-const : âˆ€{A} â†’ rep A â†’ Observe
      O-fun : Observe
      O-pair : Observe
      O-sum : Observe
      O-blame : Label â†’ Observe

    observe : âˆ€ {Î“ A} â†’ (V : Î“ âŠ¢ A) â†’ Value V â†’ Observe
    observe .(Æ› _) V-Æ› = O-fun
    observe {A = A} ($ k) V-const = O-const {A} k
    observe .(cons _ _) (V-pair v vâ‚) = O-pair
    observe .(inl _) (V-inl v) = O-sum
    observe .(inr _) (V-inr v) = O-sum
    observe (V âŸ¨ c âŸ©) (V-cast v) = observe V v

    data Eval : âˆ€ {Î“ A} â†’ (Î“ âŠ¢ A) â†’ Observe â†’ Set where
      eval : âˆ€{Î“}{A}{M V : Î“ âŠ¢ A}
           â†’ M â€”â†  V
           â†’ (v : Value V)
           â†’ Eval M (observe V v)

    {-

     The Progress data type has an additional error case to
     allow for cast errors, i.e., blame. We use the follow
     Error data type to help express the error case.

     -}

    data Error : âˆ€ {Î“ A} â†’ Î“ âŠ¢ A â†’ Set where

      E-blame : âˆ€ {Î“}{A}{â„“}
          ---------------------
        â†’ Error{Î“}{A} (blame â„“)

    data Progress {A} (M : âˆ… âŠ¢ A) : Set where

      step : âˆ€ {N : âˆ… âŠ¢ A}
        â†’ M â€”â†’ N
          -------------
        â†’ Progress M

      done :
          Value M
          ----------
        â†’ Progress M

      error :
          Error M
          ----------
        â†’ Progress M

    {-

     The proof of progress follows the same structure as the one for
     the STLC, by induction on the structure of the expression (or
     equivalently, the typing derivation). In the following, we
     discuss the extra cases that are needed for this cast calculus.

     Each recursive call to progress may result in an error,
     in which case the current expression can take a step
     via the Î¾-blame rule with an appropriate frame.

     On the other hand, if the recusive call produces a value, the
     value may be a cast that is inert. In the case for function
     application, the expression takes a step via the fun-cast rule
     (which uses the funCast parameter).  In the case for fst and snd,
     the expression takes a step via fst-cast or snd-cast
     respectively. Regarding the case form, the expression takes a
     step via case-cast.

     Of course, we must add a case for the cast form.
     If the recursive call produces a step, then we step via Î¾.
     Likewise, if the recursive call produces an error, we step via Î¾-blame.
     Otherwise, the recursive call produces a value.
     We make use of the ActiveOrInert parameter to see which
     kind of cast we are dealing with. If it is active, we reduce
     via the cast rule. Otherwise we form a value using V-cast.

     We must also consider the situations where the subexpression is
     of base type: the argument of a primitive operator and the
     condition of 'if'. In these two cases, the baseNotInert parameter
     ensures that the value not a cast, it is a constant.

     -}

    progress : âˆ€ {A} â†’ (M : âˆ… âŠ¢ A) â†’ Progress M
    progress (` ())
    progress (Æ› M) = done V-Æ›
    progress (_Â·_ {âˆ…}{A}{B} Mâ‚ Mâ‚‚)
        with progress Mâ‚
    ... | step R = step (Î¾ {F = F-Â·â‚ Mâ‚‚} R)
    ... | error E-blame = step (Î¾-blame {F = F-Â·â‚ Mâ‚‚})
    ... | done Vâ‚
            with progress Mâ‚‚
    ...     | step R' = step (Î¾ {F = (F-Â·â‚‚ Mâ‚){Vâ‚}} R')
    ...     | error E-blame = step (Î¾-blame {F = (F-Â·â‚‚ Mâ‚){Vâ‚}})
    ...     | done Vâ‚‚ with Vâ‚
    ...         | V-Æ› = step (Î² Vâ‚‚)
    ...         | V-cast {âˆ…}{A = A'}{B = A â‡’ B}{V}{c}{i} v
                with funSrc c i
    ...         | âŸ¨ Aâ‚' , âŸ¨ Aâ‚‚' , refl âŸ© âŸ© =
                    step (fun-cast v Vâ‚‚ {i})
    progress (_Â·_ {âˆ…}{A}{B} Mâ‚ Mâ‚‚) | done Vâ‚ | done Vâ‚‚
                | V-const {k = kâ‚} {f = fâ‚} with Vâ‚‚
    ...             | V-const {k = kâ‚‚} {f = fâ‚‚} =
                      step (Î´ {ab = fâ‚} {a = fâ‚‚} {b = P-Fun2 fâ‚})
    ...             | V-Æ› = contradiction fâ‚ Â¬P-Fun
    ...             | V-pair v w = contradiction fâ‚ Â¬P-Pair
    ...             | V-inl v = contradiction fâ‚ Â¬P-Sum
    ...             | V-inr v = contradiction fâ‚ Â¬P-Sum
    ...             | V-cast {âˆ…}{A'}{A}{W}{c}{i} w =
                       contradiction i (G fâ‚)
                       where G : Prim (A â‡’ B) â†’ Â¬ Inert c
                             G (P-Fun fâ‚) ic = baseNotInert c ic
    progress ($ k) = done V-const
    progress (if L M N) with progress L
    ... | step {L'} R = step (Î¾{F = F-if M N} R)
    ... | error E-blame = step (Î¾-blame{F = F-if M N})
    ... | done (V-const {k = true}) = step Î²-if-true
    ... | done (V-const {k = false}) = step Î²-if-false
    ... | done (V-cast {c = c} {i = i} v) =
            contradiction i (baseNotInert c)

    progress (_âŸ¨_âŸ© {âˆ…}{A}{B} M c) with progress M
    ... | step {N} R = step (Î¾{F = F-cast c} R)
    ... | error E-blame = step (Î¾-blame{F = F-cast c})
    ... | done v with ActiveOrInert c
    ...    | injâ‚ a = step (cast v {a})
    ...    | injâ‚‚ i = done (V-cast {c = c} {i = i} v)
    progress {Câ‚ `Ã— Câ‚‚} (cons Mâ‚ Mâ‚‚) with progress Mâ‚
    ... | step {N} R = step (Î¾ {F = F-Ã—â‚‚ Mâ‚‚} R)
    ... | error E-blame = step (Î¾-blame {F = F-Ã—â‚‚ Mâ‚‚})
    ... | done V with progress Mâ‚‚
    ...    | step {N} R' = step (Î¾ {F = F-Ã—â‚ Mâ‚} R')
    ...    | done V' = done (V-pair V V')
    ...    | error E-blame = step (Î¾-blame{F = F-Ã—â‚ Mâ‚})
    progress (fst {Î“}{A}{B} M) with progress M
    ... | step {N} R = step (Î¾ {F = F-fst} R)
    ... | error E-blame = step (Î¾-blame{F = F-fst})
    ... | done V
            with V
    ...     | V-pair {V = Vâ‚}{W = Vâ‚‚} v w = step {N = Vâ‚} (Î²-fst v w)
    ...     | V-const {k = ()}
    ...     | V-cast {c = c} {i = i} v
                with pairSrc c i
    ...         | âŸ¨ Aâ‚' , âŸ¨ Aâ‚‚' , refl âŸ© âŸ© =
                  step (fst-cast {c = c} v {i = i})
    progress (snd {Î“}{A}{B} M) with progress M
    ... | step {N} R = step (Î¾ {F = F-snd} R)
    ... | error E-blame = step (Î¾-blame{F = F-snd})
    ... | done V with V
    ...     | V-pair {V = Vâ‚}{W = Vâ‚‚} v w = step {N = Vâ‚‚} (Î²-snd v w)
    ...     | V-const {k = ()}
    ...     | V-cast {c = c} {i = i} v
                with pairSrc c i
    ...         | âŸ¨ Aâ‚' , âŸ¨ Aâ‚‚' , refl âŸ© âŸ© =
                  step (snd-cast {c = c} v {i = i})
    progress (inl M) with progress M
    ... | step R = step (Î¾ {F = F-inl} R)
    ... | error E-blame = step (Î¾-blame {F = F-inl})
    ... | done V = done (V-inl V)

    progress (inr M) with progress M
    ... | step R = step (Î¾ {F = F-inr} R)
    ... | error E-blame = step (Î¾-blame {F = F-inr})
    ... | done V = done (V-inr V)

    progress (case L M N) with progress L
    ... | step R = step (Î¾ {F = F-case M N} R)
    ... | error E-blame = step (Î¾-blame {F = F-case M N})
    ... | done V with V
    ...    | V-const {k = ()}
    ...    | V-inl v = step (Î²-caseL v)
    ...    | V-inr v = step (Î²-caseR v)
    ...    | V-cast {c = c} {i = i} v
                with sumSrc c i
    ...         | âŸ¨ Aâ‚' , âŸ¨ Aâ‚‚' , refl âŸ© âŸ© = step (case-cast {c = c} v {i = i})

    progress (blame â„“) = error E-blame

