open import Data.Nat using (â„•; zero; suc)
open import Data.Bool
open import Relation.Nullary using (Â¬_; Dec; yes; no)
open import Relation.Nullary.Negation using (contradiction)
open import Relation.Binary.PropositionalEquality
  using (_â‰¡_; _â‰¢_; refl; trans; sym; cong; congâ‚‚)
  renaming (subst to subst-eq; substâ‚‚ to substâ‚‚-eq)
open import Data.Product using (_Ã—_; projâ‚; projâ‚‚; Î£; Î£-syntax; âˆƒ; âˆƒ-syntax) renaming (_,_ to âŸ¨_,_âŸ©)
open import Data.Sum using (_âŠ_; injâ‚; injâ‚‚)
open import Data.Empty using (âŠ¥; âŠ¥-elim)

open import Types
open import Variables
open import Labels
open import CastStructureWithPrecision

module ParamGradualGuarantee (csp : CastStructWithPrecision) where

open CastStructWithPrecision csp

open import ParamCastCalculus Cast Inert
open import ParamCastAux precast
open import ParamCastReduction cs
open import ParamCCPrecision pcsp
{- We've already proven the simlulation lemmas. -}
open import ParamGradualGuaranteeSim csp



{-
  Here is the statement of the gradual guarantee:
-}
gradual-guarantee : âˆ€ {A Aâ€²} {Mâ‚ : âˆ… âŠ¢ A} {Mâ‚â€² Mâ‚‚â€² : âˆ… âŠ¢ Aâ€²}
  â†’ âˆ… , âˆ… âŠ¢ Mâ‚ âŠ‘á¶œ Mâ‚â€²     -- Note Mâ‚â€² is more precise here.
  â†’ Mâ‚â€² â€”â†’ Mâ‚‚â€²
    ---------------------------------------------
  â†’ âˆƒ[ Mâ‚‚ ] ((Mâ‚ â€”â†  Mâ‚‚) Ã— (âˆ… , âˆ… âŠ¢ Mâ‚‚ âŠ‘á¶œ Mâ‚‚â€²))

{-
  For constructors, just recur on the subterms if the reduction on the rhs is Î¾.
  Otherwise if it is a Î¾-blame, we're done and don't need to go anywhere.
-}
gradual-guarantee-cons : âˆ€ {A Aâ€² B Bâ€²} {M : âˆ… âŠ¢ A} {N : âˆ… âŠ¢ B}
                              {Mâ€² : âˆ… âŠ¢ Aâ€²} {Nâ€² : âˆ… âŠ¢ Bâ€²} {Mâ‚ : âˆ… âŠ¢ A `Ã— B} {Mâ‚â€² Mâ‚‚â€² : âˆ… âŠ¢ Aâ€² `Ã— Bâ€²}
  â†’ âˆ… , âˆ… âŠ¢ M âŠ‘á¶œ Mâ€² â†’ âˆ… , âˆ… âŠ¢ N âŠ‘á¶œ Nâ€²
  â†’ Mâ‚ â‰¡ cons M N â†’ Mâ‚â€² â‰¡ cons Mâ€² Nâ€²
  â†’ Mâ‚â€² â€”â†’ Mâ‚‚â€²
    -----------------------------------------------
  â†’ âˆƒ[ Mâ‚‚ ] ((Mâ‚ â€”â†  Mâ‚‚) Ã— (âˆ… , âˆ… âŠ¢ Mâ‚‚ âŠ‘á¶œ Mâ‚‚â€²))
gradual-guarantee-cons {M = M} {N} lpM lpN refl eq2 (Î¾ {F = F-Ã—â‚ _ vM} rd)
  with plug-inv-consâ‚ {vM = vM} eq2
... | âŸ¨ refl , refl âŸ©
  with catchup vM lpM
... | âŸ¨ V , âŸ¨  vV , âŸ¨ Mâ†’V , lpV âŸ© âŸ© âŸ© 
  with gradual-guarantee lpN rd
...   | âŸ¨ Nâ‚ , âŸ¨ rd* , lpNâ‚ âŸ© âŸ© rewrite eq2 =
    âŸ¨ cons V Nâ‚ , âŸ¨ â† -trans (plug-cong (F-Ã—â‚‚ N) Mâ†’V) (plug-cong (F-Ã—â‚ V vV) rd*) , âŠ‘á¶œ-cons lpV lpNâ‚ âŸ© âŸ©
gradual-guarantee-cons {M = M} {N} lpM lpN refl eq2 (Î¾ {F = F-Ã—â‚‚ _} rd)
  with plug-inv-consâ‚‚ eq2
... | âŸ¨ refl , refl âŸ©
  with gradual-guarantee lpM rd
...   | âŸ¨ Mâ‚ , âŸ¨ rd* , lpMâ‚ âŸ© âŸ© = âŸ¨ cons Mâ‚ N , âŸ¨ plug-cong (F-Ã—â‚‚ N) rd* , âŠ‘á¶œ-cons lpMâ‚ lpN âŸ© âŸ©
gradual-guarantee-cons {M = M} {N} lpM lpN refl eq2 (Î¾-blame {F = F-Ã—â‚ _ vM})
  with plug-inv-consâ‚ {vM = vM} eq2
... | âŸ¨ refl , refl âŸ© = âŸ¨ cons M N , âŸ¨ cons M N âˆ , âŠ‘á¶œ-blame (pairâŠ‘ (âŠ‘á¶œâ†’âŠ‘ âŠ‘*-âˆ… lpM) (âŠ‘á¶œâ†’âŠ‘ âŠ‘*-âˆ… lpN)) âŸ© âŸ©
gradual-guarantee-cons {M = M} {N} lpM lpN refl eq2 (Î¾-blame {F = F-Ã—â‚‚ _})
  with plug-inv-consâ‚‚ eq2
... | âŸ¨ refl , refl âŸ© = âŸ¨ cons M N , âŸ¨ cons M N âˆ , âŠ‘á¶œ-blame (pairâŠ‘ (âŠ‘á¶œâ†’âŠ‘ âŠ‘*-âˆ… lpM) (âŠ‘á¶œâ†’âŠ‘ âŠ‘*-âˆ… lpN)) âŸ© âŸ©

gradual-guarantee-inl : âˆ€ {A Aâ€² B Bâ€²} {M : âˆ… âŠ¢ A} {Mâ€² : âˆ… âŠ¢ Aâ€²} {Mâ‚ : âˆ… âŠ¢ A `âŠ B} {Mâ‚â€² Mâ‚‚â€² : âˆ… âŠ¢ Aâ€² `âŠ Bâ€²}
  â†’ B âŠ‘ Bâ€² â†’ âˆ… , âˆ… âŠ¢ M âŠ‘á¶œ Mâ€²
  â†’ Mâ‚ â‰¡ inl {B = B} M â†’ Mâ‚â€² â‰¡ inl {B = Bâ€²} Mâ€²
  â†’ Mâ‚â€² â€”â†’ Mâ‚‚â€²
    ---------------------------------------------
  â†’ âˆƒ[ Mâ‚‚ ] ((Mâ‚ â€”â†  Mâ‚‚) Ã— (âˆ… , âˆ… âŠ¢ Mâ‚‚ âŠ‘á¶œ Mâ‚‚â€²))
gradual-guarantee-inl lp lpM refl eq2 (Î¾ {F = F-inl} rd)
  with plug-inv-inl eq2
... | refl
  with gradual-guarantee lpM rd
...   | âŸ¨ Mâ‚ , âŸ¨ rd* , lpMâ‚ âŸ© âŸ© = âŸ¨ inl Mâ‚ , âŸ¨ plug-cong F-inl rd* , âŠ‘á¶œ-inl lp lpMâ‚ âŸ© âŸ©
gradual-guarantee-inl {M = M} lp lpM refl eq2 (Î¾-blame {F = F-inl})
  with plug-inv-inl eq2
... | refl = âŸ¨ inl M , âŸ¨ inl M âˆ , âŠ‘á¶œ-blame (sumâŠ‘ (âŠ‘á¶œâ†’âŠ‘ âŠ‘*-âˆ… lpM) lp) âŸ© âŸ©

gradual-guarantee-inr : âˆ€ {A Aâ€² B Bâ€²} {M : âˆ… âŠ¢ B} {Mâ€² : âˆ… âŠ¢ Bâ€²} {Mâ‚ : âˆ… âŠ¢ A `âŠ B} {Mâ‚â€² Mâ‚‚â€² : âˆ… âŠ¢ Aâ€² `âŠ Bâ€²}
  â†’ A âŠ‘ Aâ€² â†’ âˆ… , âˆ… âŠ¢ M âŠ‘á¶œ Mâ€²
  â†’ Mâ‚ â‰¡ inr {A = A} M â†’ Mâ‚â€² â‰¡ inr {A = Aâ€²} Mâ€²
  â†’ Mâ‚â€² â€”â†’ Mâ‚‚â€²
    ---------------------------------------------
  â†’ âˆƒ[ Mâ‚‚ ] ((Mâ‚ â€”â†  Mâ‚‚) Ã— (âˆ… , âˆ… âŠ¢ Mâ‚‚ âŠ‘á¶œ Mâ‚‚â€²))
gradual-guarantee-inr lp lpM refl eq2 (Î¾ {F = F-inr} rd)
  with plug-inv-inr eq2
... | refl
  with gradual-guarantee lpM rd
...   | âŸ¨ Mâ‚ , âŸ¨ rd* , lpMâ‚ âŸ© âŸ© = âŸ¨ inr Mâ‚ , âŸ¨ plug-cong F-inr rd* , âŠ‘á¶œ-inr lp lpMâ‚ âŸ© âŸ©
gradual-guarantee-inr {M = M} lp lpM refl eq2 (Î¾-blame {F = F-inr})
  with plug-inv-inr eq2
... | refl = âŸ¨ inr M , âŸ¨ inr M âˆ , âŠ‘á¶œ-blame (sumâŠ‘ lp (âŠ‘á¶œâ†’âŠ‘ âŠ‘*-âˆ… lpM)) âŸ© âŸ©

{-
  The term constructor `fst` is an eliminator for pairs. By casing on the reduction of the rhs,
  the Î² and cast cases are the interesting ones - we prove them in separate lemmas.
-}
gradual-guarantee-fst : âˆ€ {A Aâ€² B Bâ€²} {Nâ‚ : âˆ… âŠ¢ A `Ã— B} {Nâ‚â€² : âˆ… âŠ¢ Aâ€² `Ã— Bâ€²} {Mâ‚ : âˆ… âŠ¢ A} {Mâ‚â€² Mâ‚‚â€² : âˆ… âŠ¢ Aâ€²}
  â†’ âˆ… , âˆ… âŠ¢ Nâ‚ âŠ‘á¶œ Nâ‚â€²
  â†’ Mâ‚ â‰¡ fst Nâ‚ â†’ Mâ‚â€² â‰¡ fst Nâ‚â€²
  â†’ Mâ‚â€² â€”â†’ Mâ‚‚â€²
    -----------------------------------------------
  â†’ âˆƒ[ Mâ‚‚ ] ((Mâ‚ â€”â†  Mâ‚‚) Ã— (âˆ… , âˆ… âŠ¢ Mâ‚‚ âŠ‘á¶œ Mâ‚‚â€²))
gradual-guarantee-fst {Nâ‚ = Nâ‚} {Nâ‚â€²} {Mâ‚} {Mâ‚â€²} {Mâ‚‚â€²} Nâ‚âŠ‘Nâ‚â€² refl eq2 (Î¾ {Mâ€² = Nâ‚‚â€²} {F} Nâ‚â€²â†’Nâ‚‚â€²)
  with plug-inv-fst F eq2
... | âŸ¨ refl , âŸ¨ refl , refl âŸ© âŸ©
  with gradual-guarantee Nâ‚âŠ‘Nâ‚â€² Nâ‚â€²â†’Nâ‚‚â€²
...   | âŸ¨ Nâ‚‚ , âŸ¨ Nâ‚â† Nâ‚‚ , Nâ‚‚âŠ‘Nâ‚‚â€² âŸ© âŸ© = âŸ¨ fst Nâ‚‚ , âŸ¨ plug-cong F-fst Nâ‚â† Nâ‚‚ , âŠ‘á¶œ-fst Nâ‚‚âŠ‘Nâ‚‚â€² âŸ© âŸ©
gradual-guarantee-fst {Nâ‚ = Nâ‚} lpf refl eq2 (Î¾-blame {F = F})
  with plug-inv-fst F eq2
... | âŸ¨ refl , âŸ¨ refl , refl âŸ© âŸ© with âŠ‘á¶œâ†’âŠ‘ âŠ‘*-âˆ… lpf
...   | pairâŠ‘ lpA lpB = âŸ¨ fst Nâ‚ , âŸ¨ fst Nâ‚ âˆ , âŠ‘á¶œ-blame lpA âŸ© âŸ©
gradual-guarantee-fst lpf refl refl (Î²-fst vVâ€² vWâ€²) = sim-fst-cons vVâ€² vWâ€² lpf
gradual-guarantee-fst lpf refl refl (fst-cast vâ€² {xâ€²} {iâ€²}) = sim-fst-wrap vâ€² iâ€² xâ€² lpf

gradual-guarantee-snd : âˆ€ {A Aâ€² B Bâ€²} {Nâ‚ : âˆ… âŠ¢ A `Ã— B} {Nâ‚â€² : âˆ… âŠ¢ Aâ€² `Ã— Bâ€²} {Mâ‚ : âˆ… âŠ¢ B} {Mâ‚â€² Mâ‚‚â€² : âˆ… âŠ¢ Bâ€²}
  â†’ âˆ… , âˆ… âŠ¢ Nâ‚ âŠ‘á¶œ Nâ‚â€²
  â†’ Mâ‚ â‰¡ snd Nâ‚ â†’ Mâ‚â€² â‰¡ snd Nâ‚â€²
  â†’ Mâ‚â€² â€”â†’ Mâ‚‚â€²
    -----------------------------------------------
  â†’ âˆƒ[ Mâ‚‚ ] ((Mâ‚ â€”â†  Mâ‚‚) Ã— (âˆ… , âˆ… âŠ¢ Mâ‚‚ âŠ‘á¶œ Mâ‚‚â€²))
gradual-guarantee-snd {Nâ‚ = Nâ‚} {Nâ‚â€²} {Mâ‚} {Mâ‚â€²} {Mâ‚‚â€²} Nâ‚âŠ‘Nâ‚â€² refl eq2 (Î¾ {Mâ€² = Nâ‚‚â€²} {F} Nâ‚â€²â†’Nâ‚‚â€²)
  with plug-inv-snd F eq2
... | âŸ¨ refl , âŸ¨ refl , refl âŸ© âŸ©
  with gradual-guarantee Nâ‚âŠ‘Nâ‚â€² Nâ‚â€²â†’Nâ‚‚â€²
...   | âŸ¨ Nâ‚‚ , âŸ¨ Nâ‚â† Nâ‚‚ , Nâ‚‚âŠ‘Nâ‚‚â€² âŸ© âŸ© = âŸ¨ snd Nâ‚‚ , âŸ¨ plug-cong F-snd Nâ‚â† Nâ‚‚ , âŠ‘á¶œ-snd Nâ‚‚âŠ‘Nâ‚‚â€² âŸ© âŸ©
gradual-guarantee-snd {Nâ‚ = Nâ‚} lpNâ‚ refl eq2 (Î¾-blame {F = F})
  with plug-inv-snd F eq2
... | âŸ¨ refl , âŸ¨ refl , refl âŸ© âŸ© with âŠ‘á¶œâ†’âŠ‘ âŠ‘*-âˆ… lpNâ‚
...   | pairâŠ‘ lpA lpB = âŸ¨ snd Nâ‚ , âŸ¨ snd Nâ‚ âˆ , âŠ‘á¶œ-blame lpB âŸ© âŸ©
gradual-guarantee-snd lpNâ‚ refl refl (Î²-snd vVâ€² vWâ€²) = sim-snd-cons vVâ€² vWâ€² lpNâ‚
gradual-guarantee-snd lpNâ‚ refl refl (snd-cast vâ€² {xâ€²} {iâ€²}) = sim-snd-wrap vâ€² iâ€² xâ€² lpNâ‚

gradual-guarantee-if : âˆ€ {A Aâ€²} {L Lâ€² : âˆ… âŠ¢ ` ğ”¹} {M : âˆ… âŠ¢ A} {N : âˆ… âŠ¢ A} {Mâ€² : âˆ… âŠ¢ Aâ€²} {Nâ€² : âˆ… âŠ¢ Aâ€²} {Mâ‚ : âˆ… âŠ¢ A} {Mâ‚â€² Mâ‚‚â€² : âˆ… âŠ¢ Aâ€²}
  â†’ âˆ… , âˆ… âŠ¢ L âŠ‘á¶œ Lâ€² â†’ âˆ… , âˆ… âŠ¢ M âŠ‘á¶œ Mâ€² â†’ âˆ… , âˆ… âŠ¢ N âŠ‘á¶œ Nâ€²
  â†’ Mâ‚ â‰¡ if L M N â†’ Mâ‚â€² â‰¡ if Lâ€² Mâ€² Nâ€²
  â†’ Mâ‚â€² â€”â†’ Mâ‚‚â€²
    -----------------------------------------------
  â†’ âˆƒ[ Mâ‚‚ ] ((Mâ‚ â€”â†  Mâ‚‚) Ã— (âˆ… , âˆ… âŠ¢ Mâ‚‚ âŠ‘á¶œ Mâ‚‚â€²))
gradual-guarantee-if {L = L} {Lâ€²} {M} {N} {Mâ€²} {Nâ€²} lpL lpM lpN refl eq2 (Î¾ {F = F-if Mâ€²áµ’ Nâ€²áµ’} rd)
  with plug-inv-if eq2
... | âŸ¨ refl , âŸ¨ refl , refl âŸ© âŸ©
  with gradual-guarantee lpL rd
... | âŸ¨ Lâ‚‚ , âŸ¨ rd* , lpLâ‚‚ âŸ© âŸ© = âŸ¨ if Lâ‚‚ M N , âŸ¨ plug-cong (F-if M N) rd* , âŠ‘á¶œ-if lpLâ‚‚ lpM lpN âŸ© âŸ©
gradual-guarantee-if {L = L} {Lâ€²} {M} {N} {Mâ€²} {Nâ€²} lpL lpM lpN refl eq2 (Î¾-blame {F = F-if Mâ€²áµ’ Nâ€²áµ’})
  with plug-inv-if eq2
... | âŸ¨ refl , âŸ¨ refl , refl âŸ© âŸ© = âŸ¨ if L M N , âŸ¨ if L M N âˆ , âŠ‘á¶œ-blame (âŠ‘á¶œâ†’âŠ‘ âŠ‘*-âˆ… lpM) âŸ© âŸ©
gradual-guarantee-if {Lâ€² = .($ true)  {P-Base}} lpL lpM lpN refl refl Î²-if-true  = sim-if-true  lpL lpM
gradual-guarantee-if {Lâ€² = .($ false) {P-Base}} lpL lpM lpN refl refl Î²-if-false = sim-if-false lpL lpN

gradual-guarantee-wrap : âˆ€ {A Aâ€² B Bâ€²} {M : âˆ… âŠ¢ A} {Mâ€² : âˆ… âŠ¢ Aâ€²} {Mâ‚ : âˆ… âŠ¢ B} {Mâ‚â€² Mâ‚‚â€² : âˆ… âŠ¢ Bâ€²}
                           {c : Cast (A â‡’ B)} {câ€² : Cast (Aâ€² â‡’ Bâ€²)} {i : Inert c} {iâ€² : Inert câ€²}
  â†’ âŸª i âŸ«âŠ‘âŸª iâ€² âŸ« â†’ âˆ… , âˆ… âŠ¢ M âŠ‘á¶œ Mâ€²
  â†’ Mâ‚ â‰¡ M âŸª i âŸ« â†’ Mâ‚â€² â‰¡ Mâ€² âŸª iâ€² âŸ«
  â†’ Mâ‚â€² â€”â†’ Mâ‚‚â€²
  â†’ (B â‰¡ â‹† â†’ Bâ€² â‰¡ â‹†)
    ---------------------------------------------
  â†’ âˆƒ[ Mâ‚‚ ] ((Mâ‚ â€”â†  Mâ‚‚) Ã— (âˆ… , âˆ… âŠ¢ Mâ‚‚ âŠ‘á¶œ Mâ‚‚â€²))
gradual-guarantee-wrap {i = i} lpi lpM refl eq2 (Î¾ {F = F-wrap _} rd) imp
  with plug-inv-wrap-M eq2
... | âŸ¨ refl , refl âŸ©
  with plug-inv-wrap-i eq2
...   | âŸ¨ refl , refl âŸ©
  with gradual-guarantee lpM rd
...     | âŸ¨ Mâ‚‚ , âŸ¨ rd* , lpMâ‚‚ âŸ© âŸ© = âŸ¨ Mâ‚‚ âŸª i âŸ« , âŸ¨ plug-cong (F-wrap _) rd* , âŠ‘á¶œ-wrap lpi lpMâ‚‚ imp âŸ© âŸ©
gradual-guarantee-wrap {M = M} {i = i} lpi lpM refl eq2 (Î¾-blame {F = F-wrap _}) imp
  with plug-inv-wrap-M eq2
... | âŸ¨ refl , refl âŸ©
  with plug-inv-wrap-i eq2
...   | âŸ¨ refl , refl âŸ© = âŸ¨ M âŸª i âŸ« , âŸ¨ M âŸª i âŸ« âˆ , âŠ‘á¶œ-blame (projâ‚‚ (lpiiâ†’âŠ‘ lpi)) âŸ© âŸ©

gradual-guarantee-wrapr : âˆ€ {A Aâ€² Bâ€²} {Mâ€² : âˆ… âŠ¢ Aâ€²} {Mâ‚ : âˆ… âŠ¢ A} {Mâ‚â€² Mâ‚‚â€² : âˆ… âŠ¢ Bâ€²} {câ€² : Cast (Aâ€² â‡’ Bâ€²)} {iâ€² : Inert câ€²}
  â†’ A âŠ‘âŸª iâ€² âŸ« â†’ âˆ… , âˆ… âŠ¢ Mâ‚ âŠ‘á¶œ Mâ€²
  â†’ Mâ‚â€² â‰¡ Mâ€² âŸª iâ€² âŸ«
  â†’ Mâ‚â€² â€”â†’ Mâ‚‚â€²
  â†’ A â‰¢ â‹†
    ---------------------------------------------
  â†’ âˆƒ[ Mâ‚‚ ] ((Mâ‚ â€”â†  Mâ‚‚) Ã— (âˆ… , âˆ… âŠ¢ Mâ‚‚ âŠ‘á¶œ Mâ‚‚â€²))
-- The proofs for both cases are practically the same as `wrap`.
gradual-guarantee-wrapr lpi lpMâ‚ eq (Î¾ {F = F-wrap _} rd) Aâ‰¢â‹†
  with plug-inv-wrap-M eq
... | âŸ¨ refl , refl âŸ©
  with plug-inv-wrap-i eq
...   | âŸ¨ refl , refl âŸ©
  with gradual-guarantee lpMâ‚ rd
...     | âŸ¨ Mâ‚‚ , âŸ¨ rd* , lpMâ‚‚ âŸ© âŸ© = âŸ¨ Mâ‚‚ , âŸ¨ rd* , âŠ‘á¶œ-wrapr lpi lpMâ‚‚ Aâ‰¢â‹† âŸ© âŸ©
gradual-guarantee-wrapr {Mâ‚ = Mâ‚} lpi lpMâ‚ eq (Î¾-blame {F = F-wrap _}) Aâ‰¢â‹†
  with plug-inv-wrap-M eq
... | âŸ¨ refl , refl âŸ©
  with plug-inv-wrap-i eq
...   | âŸ¨ refl , refl âŸ© = âŸ¨ Mâ‚ , âŸ¨ Mâ‚ âˆ , âŠ‘á¶œ-blame (projâ‚‚ (lptiâ†’âŠ‘ lpi)) âŸ© âŸ©

gradual-guarantee-app : âˆ€ {A Aâ€² B Bâ€²} {L : âˆ… âŠ¢ A â‡’ B} {Lâ€² : âˆ… âŠ¢ Aâ€² â‡’ Bâ€²} {M : âˆ… âŠ¢ A} {Mâ€² : âˆ… âŠ¢ Aâ€²} {Mâ‚ : âˆ… âŠ¢ B} {Mâ‚â€² Mâ‚‚â€² : âˆ… âŠ¢ Bâ€²}
  â†’ âˆ… , âˆ… âŠ¢ L âŠ‘á¶œ Lâ€² â†’ âˆ… , âˆ… âŠ¢ M âŠ‘á¶œ Mâ€²
  â†’ Mâ‚ â‰¡ L Â· M â†’ Mâ‚â€² â‰¡ Lâ€² Â· Mâ€²
  â†’ Mâ‚â€² â€”â†’ Mâ‚‚â€²
    ---------------------------------------------
  â†’ âˆƒ[ Mâ‚‚ ] ((Mâ‚ â€”â†  Mâ‚‚) Ã— (âˆ… , âˆ… âŠ¢ Mâ‚‚ âŠ‘á¶œ Mâ‚‚â€²))
gradual-guarantee-app {M = M} lpL lpM refl eq2 (Î¾ {F = F-Â·â‚ _} rd)
  with plug-inv-appâ‚ eq2
... | âŸ¨ refl , âŸ¨ refl , refl âŸ© âŸ©
  with gradual-guarantee lpL rd
...   | âŸ¨ Lâ‚‚ , âŸ¨ rd* , lpLâ‚‚ âŸ© âŸ© = âŸ¨ Lâ‚‚ Â· M , âŸ¨ plug-cong (F-Â·â‚ _) rd* , âŠ‘á¶œ-Â· lpLâ‚‚ lpM âŸ© âŸ©
gradual-guarantee-app {L = L} {M = M} lpL lpM refl eq2 (Î¾ {F = F-Â·â‚‚ _ {v}} rd)
  with plug-inv-appâ‚‚ {vL = v} eq2
... | âŸ¨ refl , âŸ¨ refl , refl âŸ© âŸ©
  with catchup v lpL
... | âŸ¨ Lâ‚‚ , âŸ¨ vLâ‚‚ , âŸ¨ rd*â‚ , lpLâ‚‚ âŸ© âŸ© âŸ©
  with gradual-guarantee lpM rd
... | âŸ¨ Mâ‚‚ , âŸ¨ rd*â‚‚ , lpMâ‚‚ âŸ© âŸ© =
  âŸ¨ Lâ‚‚ Â· Mâ‚‚ , âŸ¨ â† -trans (plug-cong (F-Â·â‚ _) rd*â‚) (plug-cong (F-Â·â‚‚ _ {vLâ‚‚}) rd*â‚‚) , âŠ‘á¶œ-Â· lpLâ‚‚ lpMâ‚‚ âŸ© âŸ©
gradual-guarantee-app {L = L} {M = M} lpL lpM refl eq2 (Î¾-blame {F = F-Â·â‚ _})
  with plug-inv-appâ‚ eq2
... | âŸ¨ refl , âŸ¨ refl , refl âŸ© âŸ©
  with âŠ‘á¶œâ†’âŠ‘ âŠ‘*-âˆ… lpL
...   | funâŠ‘ lpA lpB = âŸ¨ L Â· M , âŸ¨ L Â· M âˆ , âŠ‘á¶œ-blame lpB âŸ© âŸ©
gradual-guarantee-app {L = L} {M = M} lpL lpM refl eq2 (Î¾-blame {F = F-Â·â‚‚ _ {v}})
  with plug-inv-appâ‚‚ {vL = v} eq2
... | âŸ¨ refl , âŸ¨ refl , refl âŸ© âŸ©
  with âŠ‘á¶œâ†’âŠ‘ âŠ‘*-âˆ… lpL
...   | funâŠ‘ lpA lpB = âŸ¨ L Â· M , âŸ¨ L Â· M âˆ , âŠ‘á¶œ-blame lpB âŸ© âŸ©
gradual-guarantee-app lpL lpM refl refl (Î² v) = sim-app-Î² v lpL lpM
gradual-guarantee-app lpL lpM refl refl Î´ = sim-app-Î´ lpL lpM
gradual-guarantee-app lpL lpM refl refl (fun-cast v w {x} {i}) = sim-app-wrap v w i x lpL lpM

gradual-guarantee-cast : âˆ€ {A Aâ€² B Bâ€²} {M : âˆ… âŠ¢ A} {Mâ€² : âˆ… âŠ¢ Aâ€²} {Mâ‚ : âˆ… âŠ¢ B} {Mâ‚â€² Mâ‚‚â€² : âˆ… âŠ¢ Bâ€²} {c : Cast (A â‡’ B)} {câ€² : Cast (Aâ€² â‡’ Bâ€²)}
  â†’ A âŠ‘ Aâ€² â†’ B âŠ‘ Bâ€² â†’ âˆ… , âˆ… âŠ¢ M âŠ‘á¶œ Mâ€²
  â†’ Mâ‚ â‰¡ M âŸ¨ c âŸ© â†’ Mâ‚â€² â‰¡ Mâ€² âŸ¨ câ€² âŸ©
  â†’ Mâ‚â€² â€”â†’ Mâ‚‚â€²
    ---------------------------------------------
  â†’ âˆƒ[ Mâ‚‚ ] ((Mâ‚ â€”â†  Mâ‚‚) Ã— (âˆ… , âˆ… âŠ¢ Mâ‚‚ âŠ‘á¶œ Mâ‚‚â€²))
gradual-guarantee-cast {c = c} lp1 lp2 lpM refl eq2 (Î¾ {F = F} rd)
  with plug-inv-cast F eq2
... | âŸ¨ refl , âŸ¨ refl , refl âŸ© âŸ©
  with gradual-guarantee lpM rd
...   | âŸ¨ N , âŸ¨ rd* , lpN âŸ© âŸ© = âŸ¨ N âŸ¨ c âŸ© , âŸ¨ plug-cong (F-cast _) rd* , âŠ‘á¶œ-cast lp1 lp2 lpN âŸ© âŸ©
gradual-guarantee-cast {M = M} {c = c} lp1 lp2 lpM refl eq2 (Î¾-blame {F = F})
  with plug-inv-cast F eq2
... | âŸ¨ refl , âŸ¨ refl , refl âŸ© âŸ© = âŸ¨ M âŸ¨ c âŸ© , âŸ¨ M âŸ¨ c âŸ© âˆ , âŠ‘á¶œ-blame lp2 âŸ© âŸ©
gradual-guarantee-cast {c = c} lp1 lp2 lpM refl refl (cast vMâ€² {aâ€²})
  with catchup vMâ€² lpM
... | âŸ¨ V , âŸ¨ v , âŸ¨ rd*â‚ , lpV âŸ© âŸ© âŸ©
  with sim-cast {c = c} v vMâ€² aâ€² lp1 lp2 lpV
...   | âŸ¨ N , âŸ¨ rd*â‚‚ , lpN âŸ© âŸ© = âŸ¨ N , âŸ¨ â† -trans (plug-cong (F-cast _) rd*â‚) rd*â‚‚ , lpN âŸ© âŸ©
gradual-guarantee-cast {c = c} lp1 lp2 lpM refl refl (wrap vMâ€² {iâ€²})
  with catchup vMâ€² lpM
... | âŸ¨ V , âŸ¨ v , âŸ¨ rd*â‚ , lpV âŸ© âŸ© âŸ©
  with sim-wrap {c = c} v vMâ€² iâ€² lp1 lp2 lpV
...   | âŸ¨ N , âŸ¨ rd*â‚‚ , lpN âŸ© âŸ© = âŸ¨ N , âŸ¨ â† -trans (plug-cong (F-cast _) rd*â‚) rd*â‚‚ , lpN âŸ© âŸ©

gradual-guarantee-castr : âˆ€ {A Aâ€² Bâ€²} {Mâ€² : âˆ… âŠ¢ Aâ€²} {M : âˆ… âŠ¢ A} {Mâ‚â€² Nâ€² : âˆ… âŠ¢ Bâ€²} {câ€² : Cast (Aâ€² â‡’ Bâ€²)}
  â†’ A âŠ‘ Aâ€² â†’ A âŠ‘ Bâ€² â†’ âˆ… , âˆ… âŠ¢ M âŠ‘á¶œ Mâ€²
  â†’ Mâ‚â€² â‰¡ plug Mâ€² (F-cast câ€²)
  â†’ Mâ‚â€² â€”â†’ Nâ€²
    ---------------------------------------------
  â†’ âˆƒ[ N ] ((M â€”â†  N) Ã— (âˆ… , âˆ… âŠ¢ N âŠ‘á¶œ Nâ€²))
gradual-guarantee-castr lp1 lp2 lpM eq (Î¾ {F = F} rd)
  with plug-inv-cast F eq
... | âŸ¨ refl , âŸ¨ refl , refl âŸ© âŸ©
  with gradual-guarantee lpM rd
...   | âŸ¨ N , âŸ¨ rd* , lpN âŸ© âŸ© = âŸ¨ N , âŸ¨ rd* , âŠ‘á¶œ-castr lp1 lp2 lpN âŸ© âŸ©
gradual-guarantee-castr lp1 lp2 lpM eq Î¾-blame = âŸ¨ _ , âŸ¨ _ âˆ , âŠ‘á¶œ-blame lp2 âŸ© âŸ©
gradual-guarantee-castr lp1 lp2 lpM refl (cast vMâ€² {aâ€²})
  with catchup vMâ€² lpM
... | âŸ¨ V , âŸ¨ v , âŸ¨ rd* , lpV âŸ© âŸ© âŸ© = âŸ¨ V , âŸ¨ rd* , castr-cast v vMâ€² aâ€² lp1 lp2 lpV âŸ© âŸ©
gradual-guarantee-castr lp1 lp2 lpM refl (wrap vMâ€² {iâ€²})
  with catchup vMâ€² lpM
... | âŸ¨ V , âŸ¨ v , âŸ¨ rd* , lpV âŸ© âŸ© âŸ© = âŸ¨ V , âŸ¨ rd* , castr-wrap v vMâ€² iâ€² lp1 lp2 lpV âŸ© âŸ©

gradual-guarantee-case : âˆ€ {A Aâ€² B Bâ€² C Câ€²}
                           {L : âˆ… âŠ¢ A `âŠ B} {M : âˆ… , A âŠ¢ C} {N : âˆ… , B âŠ¢ C} {Lâ€² : âˆ… âŠ¢ Aâ€² `âŠ Bâ€²} {Mâ€² : âˆ… , Aâ€² âŠ¢ Câ€²} {Nâ€² : âˆ… , Bâ€² âŠ¢ Câ€²}
                           {Mâ‚ : âˆ… âŠ¢ C} {Mâ‚â€² Mâ‚‚â€² : âˆ… âŠ¢ Câ€²}
  â†’ A âŠ‘ Aâ€² â†’ B âŠ‘ Bâ€²
  â†’ âˆ… , âˆ… âŠ¢ L âŠ‘á¶œ Lâ€² â†’ (âˆ… , A) , (âˆ… , Aâ€²) âŠ¢ M âŠ‘á¶œ Mâ€² â†’ (âˆ… , B) , (âˆ… , Bâ€²) âŠ¢ N âŠ‘á¶œ Nâ€²
  â†’ Mâ‚ â‰¡ case L M N â†’ Mâ‚â€² â‰¡ case Lâ€² Mâ€² Nâ€²
  â†’ Mâ‚â€² â€”â†’ Mâ‚‚â€²
    -----------------------------------------------
  â†’ âˆƒ[ Mâ‚‚ ] ((Mâ‚ â€”â†  Mâ‚‚) Ã— (âˆ… , âˆ… âŠ¢ Mâ‚‚ âŠ‘á¶œ Mâ‚‚â€²))
gradual-guarantee-case {L = L} {M} {N} {Lâ€²} {Mâ€²} {Nâ€²} lp1 lp2 lpL lpM lpN refl eq2 (Î¾ {F = F-case _ _} rd)
  with plug-inv-case eq2
... | âŸ¨ refl , âŸ¨ refl , âŸ¨ refl , âŸ¨ refl , refl âŸ© âŸ© âŸ© âŸ©
  with gradual-guarantee lpL rd
...   | âŸ¨ Lâ‚‚ , âŸ¨ rd* , lpLâ‚‚ âŸ© âŸ© = âŸ¨ case Lâ‚‚ M N , âŸ¨ plug-cong (F-case M N) rd* , âŠ‘á¶œ-case lpLâ‚‚ lp1 lp2 lpM lpN âŸ© âŸ©
gradual-guarantee-case lp1 lp2 lpL lpM lpN refl eq2 (Î¾-blame {F = F-case _ _})
  with plug-inv-case eq2
... | âŸ¨ refl , âŸ¨ refl , âŸ¨ refl , âŸ¨ refl , refl âŸ© âŸ© âŸ© âŸ© = âŸ¨ _ , âŸ¨ _ âˆ , âŠ‘á¶œ-blame (âŠ‘á¶œâ†’âŠ‘ (âŠ‘*-, lp1 âŠ‘*-âˆ…) lpM) âŸ© âŸ©
gradual-guarantee-case lp1 lp2 lpL lpM lpN refl refl (Î²-caseL v) = sim-case-caseL v lp1 lp2 lpL lpM lpN
gradual-guarantee-case lp1 lp2 lpL lpM lpN refl refl (Î²-caseR v) = sim-case-caseR v lp1 lp2 lpL lpM lpN
gradual-guarantee-case lp1 lp2 lpL lpM lpN refl refl (case-cast vâ€² {xâ€²} {iâ€²}) = sim-case-wrap vâ€² iâ€² xâ€² lp1 lp2 lpL lpM lpN

gradual-guarantee âŠ‘á¶œ-prim rd = âŠ¥-elim (VâŒ¿â†’ V-const rd)
gradual-guarantee (âŠ‘á¶œ-Æ› _ _) rd = âŠ¥-elim (VâŒ¿â†’ V-Æ› rd)
gradual-guarantee (âŠ‘á¶œ-Â· lpL lpM) rd = gradual-guarantee-app lpL lpM refl refl rd
gradual-guarantee (âŠ‘á¶œ-if lpL lpM lpN) rd = gradual-guarantee-if lpL lpM lpN refl refl rd
gradual-guarantee (âŠ‘á¶œ-cons lpM lpN) rd = gradual-guarantee-cons lpM lpN refl refl rd
gradual-guarantee (âŠ‘á¶œ-fst lpM) rd = gradual-guarantee-fst lpM refl refl rd
gradual-guarantee (âŠ‘á¶œ-snd lpM) rd = gradual-guarantee-snd lpM refl refl rd
gradual-guarantee (âŠ‘á¶œ-inl lp lpf) rd = gradual-guarantee-inl lp lpf refl refl rd
gradual-guarantee (âŠ‘á¶œ-inr lp lpf) rd = gradual-guarantee-inr lp lpf refl refl rd
gradual-guarantee (âŠ‘á¶œ-case lpL lp1 lp2 lpM lpN) rd = gradual-guarantee-case lp1 lp2 lpL lpM lpN refl refl rd
gradual-guarantee (âŠ‘á¶œ-cast lp1 lp2 lpM) rd = gradual-guarantee-cast lp1 lp2 lpM refl refl rd
gradual-guarantee (âŠ‘á¶œ-castl {c = c} lp1 lp2 lpM) rd
  with gradual-guarantee lpM rd
... | âŸ¨ Mâ‚‚ , âŸ¨ rd* , lpMâ‚‚ âŸ© âŸ© = âŸ¨ Mâ‚‚ âŸ¨ c âŸ© , âŸ¨ plug-cong (F-cast _) rd* , âŠ‘á¶œ-castl lp1 lp2 lpMâ‚‚ âŸ© âŸ©
gradual-guarantee (âŠ‘á¶œ-castr lp1 lp2 lpM) rd = gradual-guarantee-castr lp1 lp2 lpM refl rd
gradual-guarantee (âŠ‘á¶œ-wrap lpi lpM imp) rd = gradual-guarantee-wrap lpi lpM refl refl rd imp
gradual-guarantee (âŠ‘á¶œ-wrapl {i = i} lpi lpM) rd
  with gradual-guarantee lpM rd
... | âŸ¨ Mâ‚‚ , âŸ¨ rd* , lpMâ‚‚ âŸ© âŸ© = âŸ¨ Mâ‚‚ âŸª i âŸ« , âŸ¨ plug-cong (F-wrap i) rd* , âŠ‘á¶œ-wrapl lpi lpMâ‚‚ âŸ© âŸ©
gradual-guarantee (âŠ‘á¶œ-wrapr lpi lpM Aâ‰¢â‹†) rd = gradual-guarantee-wrapr lpi lpM refl rd Aâ‰¢â‹†
gradual-guarantee (âŠ‘á¶œ-blame _) rd = âŠ¥-elim (blameâŒ¿â†’ rd)
