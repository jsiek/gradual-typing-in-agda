open import Types
open import PreCastStructure
open import Labels
open import Data.Nat
open import Data.Product using (_Ã—_; projâ‚; projâ‚‚; âˆƒ; âˆƒ-syntax; Î£; Î£-syntax) renaming (_,_ to âŸ¨_,_âŸ©)
open import Data.Sum using (_âŠ_; injâ‚; injâ‚‚)
open import Data.Bool
open import Variables
open import Relation.Nullary using (Â¬_)
open import Relation.Nullary.Negation using (contradiction)
open import Relation.Binary.PropositionalEquality
  using (_â‰¡_;_â‰¢_; refl; trans; sym; cong; congâ‚‚; cong-app)
  renaming (subst to subst-eq; substâ‚‚ to substâ‚‚-eq)
open import Data.Empty using (âŠ¥; âŠ¥-elim)

{-

  This modules defines reduction for the Parameterized Cast Calculus
  and provides a proof of progress. Preservation is guaranteed in the
  way the reduction relation is defined and checked by Agda.

-}


module ParamCastAux (pcs : PreCastStruct) where

  open PreCastStruct pcs
  
  import ParamCastCalculus
  open ParamCastCalculus Cast Inert

  {-

  Before defining reduction, we first need to define Value.  In cast
  calculi, whether a cast forms a value or not depends on the shape of
  the cast. But here we have parameterized over casts.  So we must add
  more parameters to tell us whether a cast is a value-forming cast or
  not. So we add the parameter Inert to identify the later, and the
  parameter Active to identify casts that need to be reduced. Further,
  we require that all casts (at least, all the well-typed ones) can be
  categorized one of these two ways, which is given by the
  ActiveOrInert parameter.

  The following is the definition of Value. The case for casts, M âŸ¨ c âŸ©,
  requires M to be a value and c to be an inert cast.

  -}
  data Value : âˆ€ {Î“ A} â†’ Î“ âŠ¢ A â†’ Set where

    V-Æ› : âˆ€ {Î“ A B} {N : Î“ , A âŠ¢ B}
        -----------
      â†’ Value (Æ› N)

    V-const : âˆ€ {Î“} {A : Type} {k : rep A} {f : Prim A}
        ------------------------
      â†’ Value {Î“} {A} (($ k){f})

    V-pair : âˆ€ {Î“ A B} {V : Î“ âŠ¢ A} {W : Î“ âŠ¢ B}
      â†’ Value V â†’ Value W
        -----------------
      â†’ Value (cons V W)

    V-inl : âˆ€ {Î“ A B} {V : Î“ âŠ¢ A}
      â†’ Value V
        --------------------------
      â†’ Value {Î“} {A `âŠ B} (inl V)

    V-inr : âˆ€ {Î“ A B} {V : Î“ âŠ¢ B}
      â†’ Value V
        --------------------------
      â†’ Value {Î“} {A `âŠ B} (inr V)

    -- A normal cast like M âŸ¨ c âŸ© cannot be a value. Only a wrap V âŸ¨ i âŸ© is, when V is Value.
    V-wrap : âˆ€ {Î“ : Context} {A B : Type} {V : Î“ âŠ¢ A} {c : Cast (A â‡’ B)}
      â†’ Value V â†’ (i : Inert c)
        ---------------
      â†’ Value (V âŸª i âŸ«)


  {-

  A value of type â‹† must be of the form M âŸ¨ c âŸ© where c is inert cast.

  -}
  canonicalâ‹† : âˆ€ {Î“}
    â†’ (V : Î“ âŠ¢ â‹†) â†’ (Value V)
      --------------------------
    â†’ âˆƒ[ A ] âˆƒ[ Vâ€² ] (Î£[ c âˆˆ Cast (A â‡’ â‹†) ] Î£[ i âˆˆ Inert c ] (V â‰¡ (Vâ€² âŸª i âŸ«)))
  canonicalâ‹† _ (V-const {k = ()})
  canonicalâ‹† _ (V-wrap {Î“} {A} {B} {V} {c} v i) = âŸ¨ A , âŸ¨ V , âŸ¨ c , âŸ¨ i , refl âŸ© âŸ© âŸ© âŸ©

  {-

   We shall use a kind of shallow evaluation context, called a Frame,
   to collapse all of the Î¾ rules into a single rule.

  -}

  data Frame : {Î“ : Context} â†’ Type â†’ Type â†’ Set where

    F-Â·â‚ : âˆ€ {Î“ A B}
      â†’ Î“ âŠ¢ A
      â†’ Frame {Î“} (A â‡’ B) B

    F-Â·â‚‚ : âˆ€ {Î“ A B}
      â†’ (M : Î“ âŠ¢ A â‡’ B) â†’ âˆ€{v : Value {Î“} M}
      â†’ Frame {Î“} A B

    F-if : âˆ€ {Î“ A}
      â†’ Î“ âŠ¢ A
      â†’ Î“ âŠ¢ A    
      â†’ Frame {Î“} (` ğ”¹) A

    F-Ã—â‚ : âˆ€ {Î“ A B}
      â†’ Î“ âŠ¢ A
      â†’ Frame {Î“} B (A `Ã— B)

    F-Ã—â‚‚ : âˆ€ {Î“ A B}
      â†’ Î“ âŠ¢ B
      â†’ Frame {Î“} A (A `Ã— B)

    F-fst : âˆ€ {Î“ A B}
      â†’ Frame {Î“} (A `Ã— B) A

    F-snd : âˆ€ {Î“ A B}
      â†’ Frame {Î“} (A `Ã— B) B

    F-inl : âˆ€ {Î“ A B}
      â†’ Frame {Î“} A (A `âŠ B)

    F-inr : âˆ€ {Î“ A B}
      â†’ Frame {Î“} B (A `âŠ B)

    F-case : âˆ€ {Î“ A B C}
      â†’ Î“ âŠ¢ A â‡’ C
      â†’ Î“ âŠ¢ B â‡’ C
      â†’ Frame {Î“} (A `âŠ B) C

    F-cast : âˆ€ {Î“ A B}
      â†’ Cast (A â‡’ B)
      â†’ Frame {Î“} A B

    {-
      In order to satisfy progress, we need to consider the case M âŸª i âŸ«
      when M is not a Value.
    -}
    F-wrap : âˆ€ {Î“ A B} {c : Cast (A â‡’ B)}
      â†’ (i : Inert c)
      â†’ Frame {Î“} A B

  {-

  The plug function inserts an expression into the hole of a frame.

  -}

  plug : âˆ€{Î“ A B} â†’ Î“ âŠ¢ A â†’ Frame {Î“} A B â†’ Î“ âŠ¢ B
  plug L (F-Â·â‚ M)      = L Â· M
  plug M (F-Â·â‚‚ L)      = L Â· M
  plug L (F-if M N)    = if L M N
  plug L (F-Ã—â‚ M)      = cons M L
  plug M (F-Ã—â‚‚ L)      = cons M L
  plug M (F-fst)      = fst M
  plug M (F-snd)      = snd M
  plug M (F-inl)      = inl M
  plug M (F-inr)      = inr M
  plug L (F-case M N) = case L M N
  plug M (F-cast c) = M âŸ¨ c âŸ©
  plug M (F-wrap i) = M âŸª i âŸ«

  etaâ‡’ : âˆ€ {Î“ A B C D} â†’ (M : Î“ âŠ¢ A â‡’ B) 
       â†’ (c : Cast ((A â‡’ B) â‡’ (C â‡’ D)))
       â†’ (x : Cross c)
       â†’ Î“ âŠ¢ C â‡’ D
  etaâ‡’ M c x =
     Æ› (((rename S_ M) Â· ((` Z) âŸ¨ dom c x âŸ©)) âŸ¨ cod c x âŸ©)

  etaÃ— : âˆ€ {Î“ A B C D} â†’ (M : Î“ âŠ¢ A `Ã— B)
       â†’ (c : Cast ((A `Ã— B) â‡’ (C `Ã— D)))
       â†’ (x : Cross c)
       â†’ Î“ âŠ¢ C `Ã— D
  etaÃ— M c x =
     cons (fst M âŸ¨ fstC c x âŸ©) (snd M âŸ¨ sndC c x âŸ©)

  etaâŠ : âˆ€ {Î“ A B C D} â†’ (M : Î“ âŠ¢ A `âŠ B)
       â†’ (c : Cast ((A `âŠ B) â‡’ (C `âŠ D)))
       â†’ (x : Cross c)
       â†’ Î“ âŠ¢ C `âŠ D
  etaâŠ M c x =
     let l = inl ((` Z) âŸ¨ inlC c x âŸ©) in
     let r = inr ((` Z) âŸ¨ inrC c x âŸ©) in
     case M (Æ› l) (Æ› r)

  {- Here are a few inversion lemmas for `plug` : -}
  plug-inv-fst : âˆ€ {Î“ A B C} {M : Î“ âŠ¢ A `Ã— B} {N : Î“ âŠ¢ C}
    â†’ (F : Frame C A)
    â†’ plug N F â‰¡ fst M
      ----------------------------------------------------------
    â†’ Î£[ eq âˆˆ C â‰¡ A `Ã— B ] (subst-eq (Î» â–¡ â†’ Frame â–¡ A) eq F â‰¡ F-fst) Ã— (subst-eq (Î» â–¡ â†’ Î“ âŠ¢ â–¡) eq N â‰¡ M)
  plug-inv-fst F-fst refl = âŸ¨ refl , âŸ¨ refl , refl âŸ© âŸ©

  plug-inv-snd : âˆ€ {Î“ A B C} {M : Î“ âŠ¢ A `Ã— B} {N : Î“ âŠ¢ C}
    â†’ (F : Frame C B)
    â†’ plug N F â‰¡ snd M
      ----------------------------------------------------------
    â†’ Î£[ eq âˆˆ C â‰¡ A `Ã— B ] (subst-eq (Î» â–¡ â†’ Frame â–¡ B) eq F â‰¡ F-snd) Ã— (subst-eq (Î» â–¡ â†’ Î“ âŠ¢ â–¡) eq N â‰¡ M)
  plug-inv-snd F-snd refl = âŸ¨ refl , âŸ¨ refl , refl âŸ© âŸ©

  plug-inv-consâ‚ : âˆ€ {Î“ A B} {M Mâ€² : Î“ âŠ¢ A} {L Lâ€² : Î“ âŠ¢ B}
    â†’ plug L (F-Ã—â‚ M) â‰¡ cons Mâ€² Lâ€²
      -----------------------------
    â†’ (L â‰¡ Lâ€²) Ã— (M â‰¡ Mâ€²)
  plug-inv-consâ‚ refl = âŸ¨ refl , refl âŸ©

  plug-inv-consâ‚‚ : âˆ€ {Î“ A B} {M Mâ€² : Î“ âŠ¢ A} {L Lâ€² : Î“ âŠ¢ B}
    â†’ plug M (F-Ã—â‚‚ L) â‰¡ cons Mâ€² Lâ€²
      -----------------------------
    â†’ (L â‰¡ Lâ€²) Ã— (M â‰¡ Mâ€²)
  plug-inv-consâ‚‚ refl = âŸ¨ refl , refl âŸ©

  plug-inv-inl : âˆ€ {Î“ A B} {M Mâ€² : Î“ âŠ¢ A}
    â†’ plug M F-inl â‰¡ inl {B = B} Mâ€²
      ------------------------------
    â†’ M â‰¡ Mâ€²
  plug-inv-inl refl = refl

  plug-inv-inr : âˆ€ {Î“ A B} {M Mâ€² : Î“ âŠ¢ B}
    â†’ plug M F-inr â‰¡ inr {A = A} Mâ€²
      ------------------------------
    â†’ M â‰¡ Mâ€²
  plug-inv-inr refl = refl

  plug-inv-if : âˆ€ {Î“ A} {L Láµ’ : Î“ âŠ¢ ` ğ”¹} {M Máµ’ N Náµ’ : Î“ âŠ¢ A}
    â†’ plug L (F-if M N) â‰¡ if Láµ’ Máµ’ Náµ’
      --------------------------------
    â†’ (L â‰¡ Láµ’) Ã— (M â‰¡ Máµ’) Ã— (N â‰¡ Náµ’)
  plug-inv-if refl = âŸ¨ refl , âŸ¨ refl , refl âŸ© âŸ©

  {-
    Note that in the first lemma the source type can be different (A might not be the same as Aáµ’).
    When used, first perform inversion by the 1st lemma using with-abstraction and then the 2nd.
  -}
  plug-inv-wrap-M : âˆ€ {Î“ A Aáµ’ B} {M : Î“ âŠ¢ A} {Máµ’ : Î“ âŠ¢ Aáµ’} {c : Cast (A â‡’ B)} {cáµ’ : Cast (Aáµ’ â‡’ B)} {i : Inert c} {iáµ’ : Inert cáµ’}
    â†’ plug M (F-wrap i) â‰¡ Máµ’ âŸª iáµ’ âŸ«
      -----------------------------------------------------
    â†’ Î£[ eqA âˆˆ A â‰¡ Aáµ’ ] subst-eq (Î» A â†’ Î“ âŠ¢ A) eqA M â‰¡ Máµ’
  plug-inv-wrap-M refl = âŸ¨ refl , refl âŸ©

  plug-inv-wrap-i : âˆ€ {Î“ A B} {M Máµ’ : Î“ âŠ¢ A} {c cáµ’ : Cast (A â‡’ B)} {i : Inert c} {iáµ’ : Inert cáµ’}
    â†’ plug M (F-wrap i) â‰¡ Máµ’ âŸª iáµ’ âŸ«
      -------------------------------------------------------
    â†’ Î£[ eqc âˆˆ c â‰¡ cáµ’ ] subst-eq (Î» c â†’ Inert c) eqc i â‰¡ iáµ’
  plug-inv-wrap-i refl = âŸ¨ refl , refl âŸ©
