{-# OPTIONS --rewriting #-}
module LogRel.CastLogRelDir2 where

open import Data.List using (List; []; _âˆ·_; length; map)
open import Data.Nat
open import Data.Bool using (true; false) renaming (Bool to ğ”¹)
open import Data.Nat.Properties
open import Data.Product using (_,_;_Ã—_; projâ‚; projâ‚‚; Î£-syntax; âˆƒ-syntax)
open import Data.Unit using (âŠ¤; tt)
open import Data.Unit.Polymorphic renaming (âŠ¤ to topáµ–; tt to ttáµ–)
open import Data.Empty using (âŠ¥; âŠ¥-elim)
open import Data.Sum using (_âŠ_; injâ‚; injâ‚‚)
open import Relation.Binary.PropositionalEquality as Eq
  using (_â‰¡_; _â‰¢_; refl; sym; cong; subst; trans)
open import Relation.Nullary using (Â¬_; Dec; yes; no)
open import Var
open import LogRel.Cast
open import LogRel.CastPrec2
open import LogRel.CastDeterministic
open import LogRel.CastReduction
open import StepIndexedLogic
open import EquivalenceRelation

instance
  TermInhabited : Inhabited Term
  TermInhabited = record { elt = ` 0 }

data Dir : Set where
  â‰º : Dir
  â‰» : Dir

â„°âŠğ’±-type : Set
â„°âŠğ’±-type = (Prec Ã— Dir Ã— Term Ã— Term) âŠ (Prec Ã— Dir Ã— Term Ã— Term)

â„°âŠğ’±-ctx : Context
â„°âŠğ’±-ctx = â„°âŠğ’±-type âˆ· []

â„°Ë¢âŸ¦_âŸ§ : Prec â†’ Dir â†’ Term â†’ Term â†’ SetË¢ â„°âŠğ’±-ctx (cons Now âˆ…)
â„°Ë¢âŸ¦ AâŠ‘B âŸ§ dir M Mâ€² = (injâ‚‚ (AâŠ‘B , dir , M , Mâ€²)) âˆˆ zeroË¢

ğ’±Ë¢âŸ¦_âŸ§ : Prec â†’ Dir â†’ Term â†’ Term â†’ SetË¢ â„°âŠğ’±-ctx (cons Now âˆ…)
ğ’±Ë¢âŸ¦ AâŠ‘B âŸ§ dir V Vâ€² = (injâ‚ (AâŠ‘B , dir , V , Vâ€²)) âˆˆ zeroË¢

pre-ğ’± : Prec â†’ Dir â†’ Term â†’ Term â†’ SetË¢ â„°âŠğ’±-ctx (cons Later âˆ…)
pre-ğ’± (.â˜… , .â˜… , unkâŠ‘unk) dir (V âŸ¨ G !âŸ©) (Vâ€² âŸ¨ H !âŸ©)
    with G â‰¡áµ H
... | yes refl = let g = gndâ‡’ty G in
                 (Value V)Ë¢ Ã—Ë¢ (Value Vâ€²)Ë¢
                 Ã—Ë¢ (â–·Ë¢ (ğ’±Ë¢âŸ¦ (g , g , ReflâŠ‘) âŸ§ dir V Vâ€²))
... | no neq = âŠ¥ Ë¢
pre-ğ’± (.â˜… , .â˜… , unkâŠ‘unk) dir V Vâ€² = âŠ¥ Ë¢
pre-ğ’± (.â˜… , .Aâ€² , unkâŠ‘{H}{Aâ€²} d) dir (V âŸ¨ G !âŸ©) Vâ€²
    with G â‰¡áµ H
... | yes refl = (Value V)Ë¢ Ã—Ë¢ (Value Vâ€²)Ë¢
                 Ã—Ë¢ (pre-ğ’± (gndâ‡’ty G , Aâ€² , d) dir V Vâ€²)
... | no neq = âŠ¥ Ë¢
pre-ğ’± (â˜… , .Aâ€² , unkâŠ‘{H}{Aâ€²} d) dir V Vâ€² = âŠ¥ Ë¢
pre-ğ’± (.($â‚œ Î¹) , .($â‚œ Î¹) , baseâŠ‘{Î¹}) dir ($ c) ($ câ€²) = (c â‰¡ câ€²) Ë¢
pre-ğ’± (.($â‚œ Î¹) , .($â‚œ Î¹) , baseâŠ‘{Î¹}) dir V Vâ€² = âŠ¥ Ë¢
pre-ğ’± (.(A â‡’ B) , .(Aâ€² â‡’ Bâ€²) , funâŠ‘{A}{B}{Aâ€²}{Bâ€²} AâŠ‘Aâ€² BâŠ‘Bâ€²) dir (Æ› N) (Æ› Nâ€²) =
    âˆ€Ë¢[ W ] âˆ€Ë¢[ Wâ€² ] â–·Ë¢ (ğ’±Ë¢âŸ¦ (A , Aâ€² , AâŠ‘Aâ€²) âŸ§ dir W Wâ€²)
                  â†’Ë¢ â–·Ë¢ (â„°Ë¢âŸ¦ (B , Bâ€² , BâŠ‘Bâ€²) âŸ§ dir (N [ W ]) (Nâ€² [ Wâ€² ])) 
pre-ğ’± (.(A â‡’ B) , .(Aâ€² â‡’ Bâ€²) , funâŠ‘{A}{B}{Aâ€²}{Bâ€²} AâŠ‘Aâ€² BâŠ‘Bâ€²) dir V Vâ€² = âŠ¥ Ë¢
--pre-ğ’± (A , Aâ€² , AâŠ‘Aâ€²) dir V Vâ€² = âŠ¥ Ë¢

{-

   Gradual Guarantee (GG):
   
                         Mâ€² (more precise)
   M           value   blame   diverge
   value        âœ“        âœ“  
   ---------|--------|-------|--------
   blame                 âœ“   
   ---------|--------|-------|--------
   diverge               âœ“      âœ“

   We express the GG in terms of two directional
   predicates, â„°â‰º and â„°â‰», whose intersection
   is equivalent to the GG.
   
   â„°â‰º accepts the following:

                         Mâ€² (more precise)
   M           value   blame   diverge
   value         âœ“      âœ“   
   ---------|--------|-------|---------
   blame                âœ“    
   ---------|--------|-------|---------
   diverge      âœ“       âœ“       âœ“ 

-}

pre-â„° : Prec â†’ Dir â†’ Term â†’ Term â†’ SetË¢ â„°âŠğ’±-ctx (cons Later âˆ…)
pre-â„° c â‰º M Mâ€² =
   (âˆƒË¢[ N ] (M â€”â†’ N)Ë¢ Ã—Ë¢ â–·Ë¢ (â„°Ë¢âŸ¦ c âŸ§ â‰º N Mâ€²))
   âŠË¢ (Mâ€² â€”â†  blame)Ë¢   -- was ((Blame M)Ë¢ Ã—Ë¢ (Mâ€² â€”â†  blame)Ë¢)
   âŠË¢ ((Value M)Ë¢ Ã—Ë¢ ((Mâ€² â€”â†  blame)Ë¢ âŠË¢
                    (âˆƒË¢[ Vâ€² ] (Mâ€² â€”â†  Vâ€²)Ë¢ Ã—Ë¢ (Value Vâ€²)Ë¢ Ã—Ë¢ (pre-ğ’± c â‰º M Vâ€²))))

{-

   â„°â‰» accepts the following:

                         Mâ€² (more precise)
   M           value   blame   diverge
   value         âœ“       âœ“       âœ“
   ---------|--------|-------|---------
   blame                 âœ“       âœ“
   ---------|--------|-------|---------
   diverge               âœ“       âœ“

-}
pre-â„° c â‰» M Mâ€² =
   (âˆƒË¢[ Nâ€² ] (Mâ€² â€”â†’ Nâ€²)Ë¢ Ã—Ë¢ â–·Ë¢ (â„°Ë¢âŸ¦ c âŸ§ â‰» M Nâ€²))
   âŠË¢ (Blame Mâ€²)Ë¢
   âŠË¢ ((Value Mâ€²)Ë¢ Ã—Ë¢ (âˆƒË¢[ V ] (M â€”â†  V)Ë¢ Ã—Ë¢ (Value V)Ë¢ Ã—Ë¢ (pre-ğ’± c â‰» V Mâ€²)))

pre-â„°âŠğ’± : â„°âŠğ’±-type â†’ SetË¢ â„°âŠğ’±-ctx (cons Later âˆ…)
pre-â„°âŠğ’± (injâ‚ (c , dir , V , Vâ€²)) = pre-ğ’± c dir V Vâ€²
pre-â„°âŠğ’± (injâ‚‚ (c , dir , M , Mâ€²)) = pre-â„° c dir M Mâ€²

â„°âŠğ’± : â„°âŠğ’±-type â†’ Setáµ’
â„°âŠğ’± X = Î¼áµ’ pre-â„°âŠğ’± X

ğ’±âŸ¦_âŸ§ : (c : Prec) â†’ Dir â†’ Term â†’ Term â†’ Setáµ’
ğ’±âŸ¦ c âŸ§ dir V Vâ€² = â„°âŠğ’± (injâ‚ (c , dir , V , Vâ€²))

â„°âŸ¦_âŸ§ : (c : Prec) â†’ Dir â†’ Term â†’ Term â†’ Setáµ’
â„°âŸ¦ c âŸ§ dir M Mâ€² = â„°âŠğ’± (injâ‚‚ (c , dir , M , Mâ€²))

â„°-def : Prec â†’ Dir â†’ Term â†’ Term â†’ Setáµ’
â„°-def c â‰º M Mâ€² =
   (âˆƒáµ’[ N ] (M â€”â†’ N)áµ’ Ã—áµ’ â–·áµ’ (â„°âŸ¦ c âŸ§ â‰º N Mâ€²))
   âŠáµ’ (Mâ€² â€”â†  blame)áµ’  -- was ((Blame M)áµ’ Ã—áµ’ (Mâ€² â€”â†  blame)áµ’)
   âŠáµ’ ((Value M)áµ’ Ã—áµ’ ((Mâ€² â€”â†  blame)áµ’ âŠáµ’
                    (âˆƒáµ’[ Vâ€² ] (Mâ€² â€”â†  Vâ€²)áµ’ Ã—áµ’ (Value Vâ€²)áµ’ Ã—áµ’ (ğ’±âŸ¦ c âŸ§ â‰º M Vâ€²))))
â„°-def c â‰» M Mâ€² =
   (âˆƒáµ’[ Nâ€² ] (Mâ€² â€”â†’ Nâ€²)áµ’ Ã—áµ’ â–·áµ’ (â„°âŸ¦ c âŸ§ â‰» M Nâ€²))
   âŠáµ’ (Blame Mâ€²)áµ’
   âŠáµ’ ((Value Mâ€²)áµ’ Ã—áµ’ (âˆƒáµ’[ V ] (M â€”â†  V)áµ’ Ã—áµ’ (Value V)áµ’ Ã—áµ’ (ğ’±âŸ¦ c âŸ§ â‰» V Mâ€²)))

â„°-stmt : âˆ€{c}{dir}{M}{Mâ€²} â†’ â„°âŸ¦ c âŸ§ dir M Mâ€² â‰¡áµ’ â„°-def c dir M Mâ€²
â„°-stmt {c}{dir}{M}{Mâ€²} =
  â„°âŸ¦ c âŸ§ dir M Mâ€²
                 â©¦âŸ¨ â‰¡áµ’-refl refl âŸ©
  Î¼áµ’ pre-â„°âŠğ’± (Xâ‚‚ dir)
                 â©¦âŸ¨ fixpointáµ’ pre-â„°âŠğ’± (Xâ‚‚ dir) âŸ©
  # (pre-â„°âŠğ’± (Xâ‚‚ dir)) (â„°âŠğ’± , ttáµ–)
                 â©¦âŸ¨ EQ{dir} âŸ©
  â„°-def c dir M Mâ€²
  âˆ
  where
  Xâ‚ : Dir â†’ â„°âŠğ’±-type
  Xâ‚ = Î» dir â†’ injâ‚ (c , dir , M , Mâ€²)
  Xâ‚‚ = Î» dir â†’ injâ‚‚ (c , dir , M , Mâ€²)
  EQ : âˆ€{dir} â†’ # (pre-â„°âŠğ’± (Xâ‚‚ dir)) (â„°âŠğ’± , ttáµ–) â‰¡áµ’ â„°-def c dir M Mâ€²
  EQ {â‰º} = cong-âŠáµ’ (â‰¡áµ’-refl refl)
           (cong-âŠáµ’ (â‰¡áµ’-refl refl)
            (cong-Ã—áµ’ (â‰¡áµ’-refl refl) (cong-âŠáµ’ (â‰¡áµ’-refl refl)
             (cong-âˆƒ Î» Vâ€² â†’ cong-Ã—áµ’ (â‰¡áµ’-refl refl) (cong-Ã—áµ’ (â‰¡áµ’-refl refl)
              ((â‰¡áµ’-sym (fixpointáµ’ pre-â„°âŠğ’± (injâ‚ (c , â‰º , M , Vâ€²))))))))))
  EQ {â‰»} = cong-âŠáµ’ (â‰¡áµ’-refl refl) (cong-âŠáµ’ (â‰¡áµ’-refl refl)
            (cong-Ã—áµ’ (â‰¡áµ’-refl refl) (cong-âˆƒ Î» V â†’ cong-Ã—áµ’ (â‰¡áµ’-refl refl)
              (cong-Ã—áµ’ (â‰¡áµ’-refl refl)
               (â‰¡áµ’-sym (fixpointáµ’ pre-â„°âŠğ’± (injâ‚ (c , â‰» , V , Mâ€²))))))))

â„°-suc : âˆ€{c}{dir}{M}{Mâ€²}{k}
  â†’ #(â„°âŸ¦ c âŸ§ dir M Mâ€²) (suc k) â‡” #(â„°-def c dir M Mâ€²) (suc k)
â„°-suc {c}{dir}{M}{Mâ€²}{k} = â‰¡áµ’â‡’â‡”{k = suc k} (â„°-stmt{c}{dir}{M}{Mâ€²})

{----------- Relate Open Terms ------------------------------------------------}

ğ“–âŸ¦_âŸ§ : (Î“ : List Prec) â†’ Dir â†’ Subst â†’ Subst â†’ List Setáµ’
ğ“–âŸ¦ [] âŸ§ dir Ïƒ Ïƒâ€² = []
ğ“–âŸ¦ c âˆ· Î“ âŸ§ dir Ïƒ Ïƒâ€² = (ğ’±âŸ¦ c âŸ§ dir (Ïƒ 0) (Ïƒâ€² 0))
                     âˆ· ğ“–âŸ¦ Î“ âŸ§ dir (Î» x â†’ Ïƒ (suc x)) (Î» x â†’ Ïƒâ€² (suc x))

_âˆ£_âŠ¨_âŠ‘_â¦‚_ : List Prec â†’ Dir â†’ Term â†’ Term â†’ Prec â†’ Set
Î“ âˆ£ dir âŠ¨ M âŠ‘ Mâ€² â¦‚ c = âˆ€ (Î³ Î³â€² : Subst)
   â†’ ğ“–âŸ¦ Î“ âŸ§ dir Î³ Î³â€² âŠ¢áµ’ â„°âŸ¦ c âŸ§ dir (âŸª Î³ âŸ« M) (âŸª Î³â€² âŸ« Mâ€²)

_âŠ¨_âŠ‘_â¦‚_ : List Prec â†’ Term â†’ Term â†’ Prec â†’ Set
Î“ âŠ¨ M âŠ‘ Mâ€² â¦‚ c = (Î“ âˆ£ â‰º âŠ¨ M âŠ‘ Mâ€² â¦‚ c) Ã— (Î“ âˆ£ â‰» âŠ¨ M âŠ‘ Mâ€² â¦‚ c)

{----------- sanity checking ------------------------------------------------}

â„°â‰º-steps : âˆ€{c}{M}{Mâ€²}{k}
  â†’ #(â„°âŸ¦ c âŸ§ â‰º M Mâ€²) (suc k)
  â†’ (ToVal M Ã— (ToVal Mâ€² âŠ (Mâ€² â€”â†  blame)))
    âŠ (Mâ€² â€”â†  blame)
    âŠ (âˆƒ[ N ] Î£[ r âˆˆ M â€”â†  N ] len r â‰¡ k)
â„°â‰º-steps {c} {M} {Mâ€²} {zero} â„°â‰ºMMâ€²sk = injâ‚‚ (injâ‚‚ (M , (M END) , refl))
â„°â‰º-steps {c} {M} {Mâ€²} {suc k} â„°â‰ºMMâ€²sk
    with â‡”-to (â„°-suc{c}{â‰º}) â„°â‰ºMMâ€²sk
... | injâ‚‚ (injâ‚ Mâ€²â†’blame) =
      injâ‚‚ (injâ‚ Mâ€²â†’blame)
... | injâ‚‚ (injâ‚‚ (m , injâ‚ Mâ€²â†’blame)) =
      injâ‚ ((M , ((M END) , m)) , (injâ‚‚ Mâ€²â†’blame))
... | injâ‚‚ (injâ‚‚ (m , injâ‚‚ (Vâ€² , Mâ€²â†’Vâ€² , vâ€² , ğ’±â‰ºVâ€²M))) =
      injâ‚ ((M , (M END) , m) , (injâ‚ (Vâ€² , Mâ€²â†’Vâ€² , vâ€²)))
... | injâ‚ (N , Mâ†’N , â–·â„°â‰ºNMâ€²)
    with â„°â‰º-steps â–·â„°â‰ºNMâ€²
... | injâ‚ ((V , Mâ†’V , v) , injâ‚ (Vâ€² , Mâ€²â†’Vâ€² , vâ€²)) =
      injâ‚ ((V , (M â€”â†’âŸ¨ Mâ†’N âŸ© Mâ†’V) , v) , (injâ‚ (Vâ€² , Mâ€²â†’Vâ€² , vâ€²)))
... | injâ‚ ((V , Mâ†’V , v) , injâ‚‚ Mâ€²â†’blame) =
      injâ‚ ((V , (M â€”â†’âŸ¨ Mâ†’N âŸ© Mâ†’V) , v) , (injâ‚‚ Mâ€²â†’blame))
... | injâ‚‚ (injâ‚ Mâ€²â†’blame) =
      injâ‚‚ (injâ‚ Mâ€²â†’blame)
... | injâ‚‚ (injâ‚‚ (L , Nâ†’L , eq)) =
      injâ‚‚ (injâ‚‚ (L , (M â€”â†’âŸ¨ Mâ†’N âŸ© Nâ†’L) , cong suc eq))

â„°â‰»-steps : âˆ€{c}{M}{Mâ€²}{k}
  â†’ #(â„°âŸ¦ c âŸ§ â‰» M Mâ€²) (suc k)
  â†’ (ToVal M Ã— ToVal Mâ€²)
    âŠ (Mâ€² â€”â†  blame)
    âŠ (âˆƒ[ Nâ€² ] Î£[ r âˆˆ Mâ€² â€”â†  Nâ€² ] len r â‰¡ k)
â„°â‰»-steps {c} {M} {Mâ€²} {zero} â„°â‰»MMâ€²sk = injâ‚‚ (injâ‚‚ (Mâ€² , (Mâ€² END) , refl))
â„°â‰»-steps {c} {M} {Mâ€²} {suc k} â„°â‰»MMâ€²sk
    with â‡”-to (â„°-suc{c}{â‰»}) â„°â‰»MMâ€²sk
... | injâ‚‚ (injâ‚ isBlame) =
      injâ‚‚ (injâ‚ (blame END))
... | injâ‚‚ (injâ‚‚ (mâ€² , V , Mâ†’V , v , ğ’±â‰»VMâ€²)) =
      injâ‚ ((V , Mâ†’V , v) , Mâ€² , (Mâ€² END) , mâ€²)
... | injâ‚ (Nâ€² , Mâ€²â†’Nâ€² , â–·â„°â‰»MNâ€²)
    with â„°â‰»-steps â–·â„°â‰»MNâ€²
... | injâ‚ ((V , Mâ†’V , v) , (Vâ€² , Nâ€²â†’Vâ€² , vâ€²)) =
      injâ‚ ((V , Mâ†’V , v) , Vâ€² , (Mâ€² â€”â†’âŸ¨ Mâ€²â†’Nâ€² âŸ© Nâ€²â†’Vâ€²) , vâ€²)
... | injâ‚‚ (injâ‚ Nâ€²â†’blame) = injâ‚‚ (injâ‚ (Mâ€² â€”â†’âŸ¨ Mâ€²â†’Nâ€² âŸ© Nâ€²â†’blame))
... | injâ‚‚ (injâ‚‚ (Lâ€² , Nâ€²â†’Lâ€² , eq)) =
      injâ‚‚ (injâ‚‚ (Lâ€² , (Mâ€² â€”â†’âŸ¨ Mâ€²â†’Nâ€² âŸ© Nâ€²â†’Lâ€²) , cong suc eq))

{----------- â„° implies the gradual guarantee ---------------------------------}

cant-reduce-value-and-blame : âˆ€{M}{V}
   â†’ Value V
   â†’ M â€”â†  V
   â†’ M â€”â†  blame
   â†’ âŠ¥
cant-reduce-value-and-blame v (M END) (M â€”â†’âŸ¨ Mâ†’N âŸ© Nâ†’b) =
  âŠ¥-elim (value-irreducible v Mâ†’N)
cant-reduce-value-and-blame v (.blame â€”â†’âŸ¨ Mâ†’N âŸ© Nâ†’V) (.blame END) =
  âŠ¥-elim (blame-irreducible Mâ†’N)
cant-reduce-value-and-blame v (M â€”â†’âŸ¨ Mâ†’N âŸ© Nâ†’V) (.M â€”â†’âŸ¨ Mâ†’Nâ€² âŸ© Nâ€²â†’b)
  rewrite deterministic Mâ†’N Mâ†’Nâ€² = cant-reduce-value-and-blame v Nâ†’V Nâ€²â†’b

â„°â‰ºâ‰»â‡’GG : âˆ€{c}{M}{Mâ€²}
   â†’ [] âŠ¢áµ’ â„°âŸ¦ c âŸ§ â‰º M Mâ€²
   â†’ [] âŠ¢áµ’ â„°âŸ¦ c âŸ§ â‰» M Mâ€²
   â†’ âŠ¨ M âŠ‘ Mâ€²
â„°â‰ºâ‰»â‡’GG{c}{M}{Mâ€²} â„°â‰ºMMâ€² â„°â‰»MMâ€² = GG1 , GG2 , GG3 , GG4
  where
  GG1 : ToVal Mâ€² â†’ ToVal M
  GG1 (Vâ€² , Mâ€²â†’Vâ€² , vâ€²)
      with â„°â‰»-steps {k = suc (len Mâ€²â†’Vâ€²)}
                    (âŠ¢áµ’-elim â„°â‰»MMâ€² (suc (suc (len Mâ€²â†’Vâ€²))) tt)
  ... | injâ‚ ((V , Mâ†’V , v) , _) = V , Mâ†’V , v
  ... | injâ‚‚ (injâ‚ Mâ€²â†’blame) =
        âŠ¥-elim (cant-reduce-value-and-blame vâ€² Mâ€²â†’Vâ€² Mâ€²â†’blame)
  ... | injâ‚‚ (injâ‚‚ (Nâ€² , Mâ€²â†’Nâ€² , eq)) =
        âŠ¥-elim (step-value-plus-one Mâ€²â†’Nâ€² Mâ€²â†’Vâ€² vâ€² eq)

  GG2 : diverge Mâ€² â†’ diverge M
  GG2 divMâ€² k
      with â„°â‰º-steps {k = k} (âŠ¢áµ’-elim â„°â‰ºMMâ€² (suc k) tt)
  ... | injâ‚ ((V , Mâ†’V , v) , injâ‚ (Vâ€² , Mâ€²â†’Vâ€² , vâ€²)) =
        âŠ¥-elim (diverge-not-halt divMâ€² (injâ‚‚ (Vâ€² , Mâ€²â†’Vâ€² , vâ€²)))
  ... | injâ‚ ((V , Mâ†’V , v) , injâ‚‚ Mâ€²â†’blame) =
        âŠ¥-elim (diverge-not-halt divMâ€² (injâ‚ Mâ€²â†’blame))
  ... | injâ‚‚ (injâ‚ Mâ€²â†’blame) =
        âŠ¥-elim (diverge-not-halt divMâ€² (injâ‚ Mâ€²â†’blame))
  ... | injâ‚‚ (injâ‚‚ (N , Mâ†’N , eq)) = N , Mâ†’N , sym eq

  GG3 : ToVal M â†’ ToVal Mâ€² âŠ Mâ€² â€”â†  blame
  GG3 (V , Mâ†’V , v)
      with â„°â‰º-steps {k = suc (len Mâ†’V)} (âŠ¢áµ’-elim â„°â‰ºMMâ€² (suc (suc (len Mâ†’V)))tt)
  ... | injâ‚ ((V , Mâ†’V , v) , injâ‚ (Vâ€² , Mâ€²â†’Vâ€² , vâ€²)) = injâ‚ (Vâ€² , Mâ€²â†’Vâ€² , vâ€²)
  ... | injâ‚ ((V , Mâ†’V , v) , injâ‚‚ Mâ€²â†’blame) = injâ‚‚ Mâ€²â†’blame
  ... | injâ‚‚ (injâ‚ Mâ€²â†’blame) = injâ‚‚ Mâ€²â†’blame
  ... | injâ‚‚ (injâ‚‚ (N , Mâ†’N , eq)) =
        âŠ¥-elim (step-value-plus-one Mâ†’N Mâ†’V v eq)

  GG4 : diverge M â†’ divergeâŠblame Mâ€²
  GG4 divM k 
      with â„°â‰»-steps {k = k} (âŠ¢áµ’-elim â„°â‰»MMâ€² (suc k) tt)
  ... | injâ‚ ((V , Mâ†’V , v) , _) =
        âŠ¥-elim (diverge-not-halt divM (injâ‚‚ (V , Mâ†’V , v)))
  ... | injâ‚‚ (injâ‚ Mâ€²â†’blame) = blame , (Mâ€²â†’blame , (injâ‚‚ refl))
  ... | injâ‚‚ (injâ‚‚ (Nâ€² , Mâ€²â†’Nâ€² , eq)) = Nâ€² , (Mâ€²â†’Nâ€² , (injâ‚ (sym eq))) 

{----------- â„° preserved by reduction and anti-reduction (i.e. expansion) ----}

{- formulation with explicit step-indexing a la Max New -}

anti-reduction-â‰º : âˆ€{c}{M}{N}{Mâ€²}{i}
  â†’ #(â„°âŸ¦ c âŸ§ â‰º N Mâ€²) i
  â†’ (Mâ†’N : M â€”â†  N)
  â†’ #(â„°âŸ¦ c âŸ§ â‰º M Mâ€²) (len Mâ†’N + i)
anti-reduction-â‰º {c} {M} {.M} {Mâ€²} {i} â„°â‰ºNMâ€²si (.M END) = â„°â‰ºNMâ€²si
anti-reduction-â‰º {c} {M} {N} {Mâ€²} {i} â„°â‰ºNMâ€²si (_â€”â†’âŸ¨_âŸ©_ M {L}{N} Mâ†’L Lâ†’*N) =
  let IH : # (â„°âŸ¦ c âŸ§ â‰º L Mâ€²) (len Lâ†’*N + i)
      IH = anti-reduction-â‰º â„°â‰ºNMâ€²si (Lâ†’*N) in
  injâ‚ (L , Mâ†’L , IH)

anti-reduction-â‰» : âˆ€{c}{M}{Mâ€²}{Nâ€²}{i}
  â†’ #(â„°âŸ¦ c âŸ§ â‰» M Nâ€²) i
  â†’ (Mâ€²â†’Nâ€² : Mâ€² â€”â†  Nâ€²)
  â†’ #(â„°âŸ¦ c âŸ§ â‰» M Mâ€²) (len Mâ€²â†’Nâ€² + i)
anti-reduction-â‰» {c} {M} {Mâ€²} {.Mâ€²} {i} â„°â‰»MNâ€² (.Mâ€² END) = â„°â‰»MNâ€²
anti-reduction-â‰» {c} {M} {Mâ€²}{Nâ€²} {i} â„°â‰»MNâ€² (_â€”â†’âŸ¨_âŸ©_ Mâ€² {Lâ€²}{Nâ€²} Mâ€²â†’Lâ€² Lâ€²â†’*Nâ€²)=
  let IH : # (â„°âŸ¦ c âŸ§ â‰» M Lâ€²) (len Lâ€²â†’*Nâ€² + i)
      IH = anti-reduction-â‰» â„°â‰»MNâ€² (Lâ€²â†’*Nâ€²) in
  injâ‚ (Lâ€² , Mâ€²â†’Lâ€² , IH)

anti-reduction-â‰º-R : âˆ€{c}{M}{Mâ€²}{Nâ€²}{i}
  â†’ #(â„°âŸ¦ c âŸ§ â‰º M Nâ€²) i
  â†’ (Mâ€²â†’Nâ€² : Mâ€² â€”â†  Nâ€²)
  â†’ #(â„°âŸ¦ c âŸ§ â‰º M Mâ€²) i
anti-reduction-â‰º-R {c}{M}{Mâ€²}{Nâ€²}{zero} â„°MNâ€² Mâ€²â†’Nâ€² = tz (â„°âŸ¦ c âŸ§ â‰º M Mâ€²)
anti-reduction-â‰º-R {c}{M}{Mâ€²}{Nâ€²}{suc i} â„°MNâ€² Mâ€²â†’Nâ€²
    with â„°MNâ€²
... | injâ‚ (N , Mâ†’N , â–·â„°NNâ€²) =
         let â„°NMâ€²si = anti-reduction-â‰º-R â–·â„°NNâ€² Mâ€²â†’Nâ€² in
         injâ‚ (N , Mâ†’N , â„°NMâ€²si)
... | injâ‚‚ (injâ‚ Nâ€²â†’blame) = injâ‚‚ (injâ‚ (Mâ€²â†’Nâ€² ++ Nâ€²â†’blame))
... | injâ‚‚ (injâ‚‚ (m , injâ‚ Nâ€²â†’blame)) = injâ‚‚ (injâ‚ (Mâ€²â†’Nâ€² ++ Nâ€²â†’blame))
... | injâ‚‚ (injâ‚‚ (m , injâ‚‚ (Vâ€² , Nâ€²â†’Vâ€² , vâ€² , ğ’±MVâ€²))) =
      injâ‚‚ (injâ‚‚ (m , injâ‚‚ (Vâ€² , (Mâ€²â†’Nâ€² ++ Nâ€²â†’Vâ€²) , vâ€² , ğ’±MVâ€²)))

anti-reduction-â‰»-L : âˆ€{c}{M}{N}{Mâ€²}{i}
  â†’ #(â„°âŸ¦ c âŸ§ â‰» N Mâ€²) i
  â†’ (Mâ†’N : M â€”â†  N)
  â†’ #(â„°âŸ¦ c âŸ§ â‰» M Mâ€²) i
anti-reduction-â‰»-L {c} {M} {Mâ€²} {Nâ€²} {zero} â„°NMâ€² Mâ†’N = tz (â„°âŸ¦ c âŸ§ â‰» M Nâ€²)
anti-reduction-â‰»-L {c} {M} {N}{Mâ€²}  {suc i} â„°NMâ€² Mâ†’N
    with â„°NMâ€²
... | injâ‚ (Nâ€² , Mâ€²â†’Nâ€² , â–·â„°MNâ€²) =
      injâ‚ (Nâ€² , (Mâ€²â†’Nâ€² , (anti-reduction-â‰»-L â–·â„°MNâ€² Mâ†’N)))
... | injâ‚‚ (injâ‚ isBlame) = injâ‚‚ (injâ‚ isBlame)
... | injâ‚‚ (injâ‚‚ (mâ€² , V , Nâ†’V , v , ğ’±VMâ€²)) =
      injâ‚‚ (injâ‚‚ (mâ€² , V , (Mâ†’N ++ Nâ†’V) , v , ğ’±VMâ€²))

{-
â„°-anti-reduction-L : âˆ€{c}{M}{N}{Mâ€²}{i}{dir}
  â†’ #(â„°âŸ¦ c âŸ§ dir N Mâ€²) i
  â†’ (Mâ†’N : M â€”â†’ N)
  â†’ #(â„°âŸ¦ c âŸ§ dir M Mâ€²) (suc i)
â„°-anti-reduction-L {c} {M} {N} {Mâ€²} {i} {â‰º} â„°NMâ€² Mâ†’N =
    let xx = anti-reduction-â‰º â„°NMâ€² (unit Mâ†’N) in
    xx
â„°-anti-reduction-L {c} {M} {N} {Mâ€²} {i} {â‰»} â„°NMâ€² Mâ†’N =
    let xx = anti-reduction-â‰»-L â„°NMâ€² (unit Mâ†’N) in
    {!!}
-}

anti-reduction : âˆ€{c}{M}{N}{Mâ€²}{Nâ€²}{i}{dir}
  â†’ #(â„°âŸ¦ c âŸ§ dir N Nâ€²) i
  â†’ (Mâ†’N : M â€”â†’ N)
  â†’ (Mâ€²â†’Nâ€² : Mâ€² â€”â†’ Nâ€²)
  â†’ #(â„°âŸ¦ c âŸ§ dir M Mâ€²) (suc i)
anti-reduction {c} {M} {N} {Mâ€²} {Nâ€²} {i} {â‰º} â„°NNâ€²i Mâ†’N Mâ€²â†’Nâ€² =
  let â„°MNâ€²si = anti-reduction-â‰º â„°NNâ€²i (unit Mâ†’N) in
  let â„°Mâ€²Nâ€²si = anti-reduction-â‰º-R â„°MNâ€²si (unit Mâ€²â†’Nâ€²) in
  â„°Mâ€²Nâ€²si
anti-reduction {c} {M} {N} {Mâ€²} {Nâ€²} {i} {â‰»} â„°NNâ€²i Mâ†’N Mâ€²â†’Nâ€² =
  let â„°Mâ€²Nsi = anti-reduction-â‰» â„°NNâ€²i (unit Mâ€²â†’Nâ€²) in
  let â„°Mâ€²Nâ€²si = anti-reduction-â‰»-L â„°Mâ€²Nsi (unit Mâ†’N) in
  â„°Mâ€²Nâ€²si

reduction-â‰º : âˆ€{c}{M}{N}{Mâ€²}{i}
  â†’ #(â„°âŸ¦ c âŸ§ â‰º M Mâ€²) (suc i)
  â†’ (Mâ†’N : M â€”â†’ N)
  â†’ #(â„°âŸ¦ c âŸ§ â‰º N Mâ€²) i
reduction-â‰º {c} {M} {N} {Mâ€²} {zero} â„°MMâ€²si Mâ†’N = tz (â„°âŸ¦ c âŸ§ â‰º N Mâ€²)
reduction-â‰º {c} {M} {N} {Mâ€²} {suc i} â„°MMâ€²ssi Mâ†’N
    with â„°MMâ€²ssi
... | injâ‚ (Nâ‚‚ , Mâ†’Nâ‚‚ , â–·â„°Nâ‚‚Mâ€²) rewrite deterministic Mâ†’N Mâ†’Nâ‚‚ = â–·â„°Nâ‚‚Mâ€²
... | injâ‚‚ (injâ‚ Mâ€²â†’blame) =
      injâ‚‚ (injâ‚ Mâ€²â†’blame)
... | injâ‚‚ (injâ‚‚ (m , _)) =
      âŠ¥-elim (value-irreducible m Mâ†’N)

reduction*-â‰º : âˆ€{c}{M}{N}{Mâ€²}{i}
  â†’ (Mâ†’N : M â€”â†  N)
  â†’ #(â„°âŸ¦ c âŸ§ â‰º M Mâ€²) (len Mâ†’N + i)
  â†’ #(â„°âŸ¦ c âŸ§ â‰º N Mâ€²) i
reduction*-â‰º {c} {M} {.M} {Mâ€²} {i} (.M END) â„°MMâ€² = â„°MMâ€²
reduction*-â‰º {c} {M} {N} {Mâ€²} {i} (.M â€”â†’âŸ¨ Mâ†’L âŸ© Lâ†’N) â„°MMâ€² =
  let â„°LMâ€² = reduction-â‰º â„°MMâ€² Mâ†’L in
  reduction*-â‰º Lâ†’N â„°LMâ€² 

reduction-â‰» : âˆ€{c}{M}{N}{Mâ€²}{i}
  â†’ #(â„°âŸ¦ c âŸ§ â‰» M Mâ€²) (suc i)
  â†’ (Mâ†’N : M â€”â†’ N)
  â†’ #(â„°âŸ¦ c âŸ§ â‰» N Mâ€²) i
reduction-â‰» {c} {M} {N} {Mâ€²} {zero} â„°MMâ€²si Mâ†’N = tz (â„°âŸ¦ c âŸ§ â‰» N Mâ€²)
reduction-â‰» {c} {M} {N} {Mâ€²} {suc i} â„°MMâ€²si Mâ†’N
    with â„°MMâ€²si
... | injâ‚ (Nâ€² , Mâ€²â†’Nâ€² , â–·â„°MNâ€²) =
      injâ‚ (Nâ€² , Mâ€²â†’Nâ€² , reduction-â‰» â–·â„°MNâ€² Mâ†’N)
... | injâ‚‚ (injâ‚ Mâ€²â†’blame) = injâ‚‚ (injâ‚ Mâ€²â†’blame)
... | injâ‚‚ (injâ‚‚ (mâ€² , V , (.V END) , v , ğ’±VMâ€²)) =
      âŠ¥-elim (value-irreducible v Mâ†’N)
... | injâ‚‚ (injâ‚‚ (mâ€² , V , (.M â€”â†’âŸ¨ Mâ†’Mâ‚ âŸ© Mâ‚â†’V) , v , ğ’±VMâ€²))
    with deterministic Mâ†’N Mâ†’Mâ‚
... | refl =
    let ğ’±VMâ€²si = down (ğ’±âŸ¦ c âŸ§ â‰» V Mâ€²) (suc (suc i)) ğ’±VMâ€² (suc i) (nâ‰¤1+n _) in
    injâ‚‚ (injâ‚‚ (mâ€² , V , Mâ‚â†’V , v , ğ’±VMâ€²si)) 

â„°-reduction : âˆ€{c}{M}{N}{Mâ€²}{i}{dir}
  â†’ #(â„°âŸ¦ c âŸ§ dir M Mâ€²) (suc i)
  â†’ (Mâ†’N : M â€”â†’ N)
  â†’ #(â„°âŸ¦ c âŸ§ dir N Mâ€²) i
â„°-reduction {c} {M} {N} {Mâ€²} {i} {â‰º} â„°MMâ€² Mâ†’N = reduction-â‰º â„°MMâ€² Mâ†’N
â„°-reduction {c} {M} {N} {Mâ€²} {i} {â‰»} â„°MMâ€² Mâ†’N = reduction-â‰» â„°MMâ€² Mâ†’N

{- logical formulation -}

expansion-â–·-â‰º : âˆ€{ğ’«}{c}{M}{N}{Mâ€²}
  â†’ ğ’« âŠ¢áµ’ â–·áµ’ (â„°âŸ¦ c âŸ§ â‰º N Mâ€²)
  â†’ M â€”â†’ N
  â†’ ğ’« âŠ¢áµ’ â„°âŸ¦ c âŸ§ â‰º M Mâ€²
expansion-â–·-â‰º {ğ’«}{c}{M}{N}{Mâ€²} âŠ¢â–·â„°NMâ€² Mâ†’N =
  substáµ’ (â‰¡áµ’-sym (â„°-stmt{c}{â‰º}{M}{Mâ€²}))
  (injâ‚áµ’ (âŠ¢áµ’-âˆƒ-intro N (constáµ’I Mâ†’N ,áµ’ âŠ¢â–·â„°NMâ€²)))

expansion-â–·-â‰» : âˆ€{ğ’«}{c}{M}{Mâ€²}{Nâ€²}
  â†’ ğ’« âŠ¢áµ’ â–·áµ’ (â„°âŸ¦ c âŸ§ â‰» M Nâ€²)
  â†’ Mâ€² â€”â†’ Nâ€²
  â†’ ğ’« âŠ¢áµ’ â„°âŸ¦ c âŸ§ â‰» M Mâ€²
expansion-â–·-â‰» {ğ’«}{c}{M}{Mâ€²}{Nâ€²} âŠ¢â–·â„°MNâ€² Mâ€²â†’Nâ€² =
  substáµ’ (â‰¡áµ’-sym (â„°-stmt{c}{â‰»}{M}{Mâ€²}))
  (injâ‚áµ’ (âŠ¢áµ’-âˆƒ-intro Nâ€² (constáµ’I Mâ€²â†’Nâ€² ,áµ’ âŠ¢â–·â„°MNâ€²)))

â„°-blame-step : âˆ€{c}{dir}{M}{k}
   â†’ #(â„°âŸ¦ c âŸ§ dir M blame) k
â„°-blame-step {c} {dir} {M} {zero} = tz (â„°âŸ¦ c âŸ§ dir M blame)
â„°-blame-step {c} {â‰º} {M} {suc k} = injâ‚‚ (injâ‚ (blame END))
â„°-blame-step {c} {â‰»} {M} {suc k} = injâ‚‚ (injâ‚ isBlame)

{------------- Related values are syntactic values ----------------------------}

ğ’±â‡’Value : âˆ€ {k}{dir} c M Mâ€²
   â†’ # (ğ’±âŸ¦ c âŸ§ dir M Mâ€²) (suc k)
     ----------------------------
   â†’ Value M Ã— Value Mâ€²
ğ’±â‡’Value {k} (.â˜… , â˜… , unkâŠ‘unk) (V âŸ¨ G !âŸ©) (Vâ€² âŸ¨ H !âŸ©) ğ’±MMâ€²
    with G â‰¡áµ H
... | no neq = âŠ¥-elim ğ’±MMâ€²
... | yes refl
    with ğ’±MMâ€²
... | v , vâ€² , _ = (v âŒ© G âŒª) , (vâ€² âŒ© G âŒª)
ğ’±â‡’Value {k} (â˜… , Aâ€² , unkâŠ‘{H}{Aâ€²} d) (V âŸ¨ G !âŸ©) Vâ€² ğ’±VGVâ€²
    with G â‰¡áµ H
... | yes refl
    with ğ’±VGVâ€²
... | v , vâ€² , _ = (v âŒ© _ âŒª) , vâ€²
ğ’±â‡’Value {k} (â˜… , Aâ€² , unkâŠ‘{H}{Aâ€²} d) (V âŸ¨ G !âŸ©) Vâ€² ğ’±VGVâ€²
    | no neq = âŠ¥-elim ğ’±VGVâ€²
ğ’±â‡’Value {k} ($â‚œ Î¹ , $â‚œ Î¹ , baseâŠ‘) ($ c) ($ câ€²) refl = ($Ì¬ c) , ($Ì¬ c)
ğ’±â‡’Value {k} ((A â‡’ B) , (Aâ€² â‡’ Bâ€²) , funâŠ‘ AâŠ‘Aâ€² BâŠ‘Bâ€²) (Æ› N) (Æ› Nâ€²) ğ’±VVâ€² =
    (Æ›Ì¬ N) , (Æ›Ì¬ Nâ€²)

{--------- Equations, intro, and elim rules for ğ’± ----------------------------}

ğ’±-base : âˆ€{Î¹}{c}{câ€²}{dir}
   â†’ ğ’±âŸ¦ ($â‚œ Î¹ , $â‚œ Î¹ , baseâŠ‘) âŸ§ dir ($ c) ($ câ€²) â‰¡áµ’ (c â‰¡ câ€²) áµ’
ğ’±-base{Î¹}{c}{câ€²} = â‰¡áµ’-intro Î» k â†’ (Î» x â†’ x) , (Î» x â†’ x)

ğ’±-base-intro : âˆ€{ğ’«}{Î¹}{c}{dir}
   â†’ ğ’« âŠ¢áµ’ ğ’±âŸ¦ ($â‚œ Î¹ , $â‚œ Î¹ , baseâŠ‘) âŸ§ dir ($ c) ($ c)
ğ’±-base-intro{Î¹}{c} = substáµ’ (â‰¡áµ’-sym ğ’±-base) (constáµ’I refl)

ğ’±-base-elim-step : âˆ€{Î¹}{Î¹â€²}{c}{V}{Vâ€²}{dir}{k}
  â†’ #(ğ’±âŸ¦ $â‚œ Î¹ , $â‚œ Î¹â€² , c âŸ§ dir V Vâ€²) (suc k)
  â†’ âˆƒ[ c ] Î¹ â‰¡ Î¹â€² Ã— V â‰¡ $ c Ã— Vâ€² â‰¡ $ c
ğ’±-base-elim-step {Î¹} {.Î¹} {baseâŠ‘} {$ c} {$ câ€²} {dir} {k} refl =
  c , refl , refl , refl

ğ’±-base-elim : âˆ€{ğ’«}{Î¹}{Î¹â€²}{c}{V}{Vâ€²}{R}{dir}
  â†’ ğ’« âŠ¢áµ’ ğ’±âŸ¦ $â‚œ Î¹ , $â‚œ Î¹â€² , c âŸ§ dir V Vâ€²
  â†’ (âˆ€ k â†’ Î¹ â‰¡ Î¹â€² â†’ V â‰¡ $ k â†’ Vâ€² â‰¡ $ k â†’ ğ’« âŠ¢áµ’ R)
    -------------------------------------------
  â†’ ğ’« âŠ¢áµ’ R
ğ’±-base-elim{ğ’«}{Î¹}{Î¹â€²}{c}{V}{Vâ€²}{R} âŠ¢ğ’±VVâ€² cont =
  âŠ¢áµ’-sucP âŠ¢ğ’±VVâ€² Î» ğ’±VVâ€² â†’ G ğ’±VVâ€² âŠ¢ğ’±VVâ€² cont
  where
  G : âˆ€{Î¹}{Î¹â€²}{c}{V}{Vâ€²}{n}{dir} â†’  #(ğ’±âŸ¦ $â‚œ Î¹ , $â‚œ Î¹â€² , c âŸ§ dir V Vâ€²) (suc n)
    â†’ ğ’« âŠ¢áµ’ ğ’±âŸ¦ $â‚œ Î¹ , $â‚œ Î¹â€² , c âŸ§ dir V Vâ€²
    â†’ (âˆ€ k â†’ Î¹ â‰¡ Î¹â€² â†’ V â‰¡ $ k â†’ Vâ€² â‰¡ $ k â†’ ğ’« âŠ¢áµ’ R)
    â†’ ğ’« âŠ¢áµ’ R
  G {Î¹} {.Î¹} {baseâŠ‘} {$ k} {$ kâ€²} {n}{dir} refl âŠ¢ğ’±kk cont =
     cont k refl refl refl

ğ’±-fun : âˆ€{A B Aâ€² Bâ€²}{AâŠ‘Aâ€² : A âŠ‘ Aâ€²}{BâŠ‘Bâ€² : B âŠ‘ Bâ€²}{N}{Nâ€²}{dir}
   â†’ (ğ’±âŸ¦ A â‡’ B , Aâ€² â‡’ Bâ€² , funâŠ‘ AâŠ‘Aâ€² BâŠ‘Bâ€² âŸ§ dir (Æ› N) (Æ› Nâ€²))
      â‰¡áµ’ (âˆ€áµ’[ W ] âˆ€áµ’[ Wâ€² ] ((â–·áµ’ (ğ’±âŸ¦ A , Aâ€² , AâŠ‘Aâ€² âŸ§ dir W Wâ€²))
                       â†’áµ’ (â–·áµ’ (â„°âŸ¦ B , Bâ€² , BâŠ‘Bâ€² âŸ§ dir (N [ W ]) (Nâ€² [ Wâ€² ])))))
ğ’±-fun {A}{B}{Aâ€²}{Bâ€²}{AâŠ‘Aâ€²}{BâŠ‘Bâ€²}{N}{Nâ€²}{dir} =
   let X = injâ‚ ((A â‡’ B , Aâ€² â‡’ Bâ€² , funâŠ‘ AâŠ‘Aâ€² BâŠ‘Bâ€²) , dir , Æ› N , Æ› Nâ€²) in
   (ğ’±âŸ¦ A â‡’ B , Aâ€² â‡’ Bâ€² , funâŠ‘ AâŠ‘Aâ€² BâŠ‘Bâ€² âŸ§ dir (Æ› N) (Æ› Nâ€²))  â©¦âŸ¨ â‰¡áµ’-refl refl âŸ©
   â„°âŠğ’± X                                              â©¦âŸ¨ fixpointáµ’ pre-â„°âŠğ’± X âŸ©
   # (pre-â„°âŠğ’± X) (â„°âŠğ’± , ttáµ–)                                 â©¦âŸ¨ â‰¡áµ’-refl refl âŸ©
   (âˆ€áµ’[ W ] âˆ€áµ’[ Wâ€² ] ((â–·áµ’ (ğ’±âŸ¦ A , Aâ€² , AâŠ‘Aâ€² âŸ§ dir W Wâ€²))
                 â†’áµ’ (â–·áµ’ (â„°âŸ¦ B , Bâ€² , BâŠ‘Bâ€² âŸ§ dir (N [ W ]) (Nâ€² [ Wâ€² ]))))) âˆ

ğ’±-fun-elim-step : âˆ€{A}{B}{Aâ€²}{Bâ€²}{c : A âŠ‘ Aâ€²}{d : B âŠ‘ Bâ€²}{V}{Vâ€²}{dir}{k}{j}
  â†’ #(ğ’±âŸ¦ A â‡’ B , Aâ€² â‡’ Bâ€² , funâŠ‘ c d âŸ§ dir V Vâ€²) (suc k)
  â†’ j â‰¤ k
  â†’ âˆƒ[ N ] âˆƒ[ Nâ€² ] V â‰¡ Æ› N Ã— Vâ€² â‰¡ Æ› Nâ€² 
      Ã— (âˆ€{W Wâ€²} â†’ # (ğ’±âŸ¦ A , Aâ€² , c âŸ§ dir W Wâ€²) j
                 â†’ # (â„°âŸ¦ B , Bâ€² , d âŸ§ dir (N [ W ]) (Nâ€² [ Wâ€² ])) j)
ğ’±-fun-elim-step {A}{B}{Aâ€²}{Bâ€²}{c}{d}{Æ› N}{Æ› Nâ€²}{dir}{k}{j} ğ’±VVâ€² jâ‰¤k =
  N , Nâ€² , refl , refl , Î» {W}{Wâ€²} ğ’±WWâ€² â†’
    let ğ’±Î»NÎ»Nâ€²sj = down (ğ’±âŸ¦ A â‡’ B , Aâ€² â‡’ Bâ€² , funâŠ‘ c d âŸ§ dir (Æ› N) (Æ› Nâ€²))
                        (suc k) ğ’±VVâ€² (suc j) (sâ‰¤s jâ‰¤k) in
    let â„°NWNâ€²Wâ€²j = ğ’±Î»NÎ»Nâ€²sj W Wâ€² (suc j) â‰¤-refl ğ’±WWâ€² in
    â„°NWNâ€²Wâ€²j

ğ’±-fun-elim : âˆ€{ğ’«}{A}{B}{Aâ€²}{Bâ€²}{c : A âŠ‘ Aâ€²}{d : B âŠ‘ Bâ€²}{V}{Vâ€²}{R}{dir}
   â†’ ğ’« âŠ¢áµ’ ğ’±âŸ¦ A â‡’ B , Aâ€² â‡’ Bâ€² , funâŠ‘ c d âŸ§ dir V Vâ€²
   â†’ (âˆ€ N Nâ€² â†’ V â‰¡ Æ› N â†’ Vâ€² â‰¡ Æ› Nâ€² 
             â†’ (âˆ€{W Wâ€²} â†’ ğ’« âŠ¢áµ’ (â–·áµ’ (ğ’±âŸ¦ A , Aâ€² , c âŸ§ dir W Wâ€²))
                          â†’áµ’ (â–·áµ’ (â„°âŸ¦ B , Bâ€² , d âŸ§ dir (N [ W ]) (Nâ€² [ Wâ€² ]))))
             â†’ ğ’« âŠ¢áµ’ R)
     --------------------------------------------------------------------
   â†’ ğ’« âŠ¢áµ’ R
ğ’±-fun-elim {ğ’«}{A}{B}{Aâ€²}{Bâ€²}{c}{d}{V}{Vâ€²}{R}{dir} âŠ¢ğ’±VVâ€² cont =
  âŠ¢áµ’-sucP âŠ¢ğ’±VVâ€² Î» { ğ’±VVâ€²sn â†’ G {V}{Vâ€²} ğ’±VVâ€²sn âŠ¢ğ’±VVâ€² cont }
  where
  G : âˆ€{V}{Vâ€²}{n}
     â†’ # (ğ’±âŸ¦  A â‡’ B , Aâ€² â‡’ Bâ€² , funâŠ‘ c d âŸ§ dir V Vâ€²) (suc n)
     â†’ ğ’« âŠ¢áµ’ ğ’±âŸ¦ A â‡’ B , Aâ€² â‡’ Bâ€² , funâŠ‘ c d âŸ§ dir V Vâ€²
     â†’ (âˆ€ N Nâ€² â†’ V â‰¡ Æ› N â†’ Vâ€² â‰¡ Æ› Nâ€² 
             â†’ (âˆ€{W Wâ€²} â†’ ğ’« âŠ¢áµ’ (â–·áµ’ (ğ’±âŸ¦ A , Aâ€² , c âŸ§ dir W Wâ€²))
                           â†’áµ’ (â–·áµ’ (â„°âŸ¦ B , Bâ€² , d âŸ§ dir (N [ W ]) (Nâ€² [ Wâ€² ]))))
             â†’ ğ’« âŠ¢áµ’ R)
     â†’ ğ’« âŠ¢áµ’ R
  G {Æ› N}{Æ› Nâ€²}{n} ğ’±VVâ€² âŠ¢ğ’±VVâ€² cont = cont N Nâ€² refl refl Î» {W}{Wâ€²} â†’
     instáµ’ (instáµ’ (substáµ’ ğ’±-fun âŠ¢ğ’±VVâ€²) W) Wâ€² 

ğ’±-dyn-any : âˆ€{V}{Vâ€²}{G}{Aâ€²}{dir}{d : gndâ‡’ty G âŠ‘ Aâ€²}
   â†’ ğ’±âŸ¦ â˜… , Aâ€² , unkâŠ‘{G}{Aâ€²} d âŸ§ dir (V âŸ¨ G !âŸ©) Vâ€² 
     â‰¡áµ’ (Value V)áµ’ Ã—áµ’ (Value Vâ€²)áµ’ Ã—áµ’ (ğ’±âŸ¦ (gndâ‡’ty G , Aâ€² , d) âŸ§ dir V Vâ€²)
ğ’±-dyn-any {V}{Vâ€²}{G}{Aâ€²}{dir}{d} = 
  ğ’±âŸ¦ â˜… , Aâ€² , unkâŠ‘ d âŸ§ dir (V âŸ¨ G !âŸ©) Vâ€²
     â©¦âŸ¨ â‰¡áµ’-refl refl âŸ©
  â„°âŠğ’± X
    â©¦âŸ¨ fixpointáµ’ pre-â„°âŠğ’± X  âŸ©
  # (pre-â„°âŠğ’± X) (â„°âŠğ’± , ttáµ–)
    â©¦âŸ¨ Goal âŸ©
  (Value V)áµ’ Ã—áµ’ (Value Vâ€²)áµ’ Ã—áµ’ (ğ’±âŸ¦ (gndâ‡’ty G , Aâ€² , d) âŸ§ dir V Vâ€²)
  âˆ
  where
  X = injâ‚ ((â˜… , Aâ€² , unkâŠ‘ d) , dir , (V âŸ¨ G !âŸ©) , Vâ€²)
  Goal : # (pre-â„°âŠğ’± X) (â„°âŠğ’± , ttáµ–)
         â‰¡áµ’ (Value V)áµ’ Ã—áµ’ (Value Vâ€²)áµ’ Ã—áµ’ (ğ’±âŸ¦ (gndâ‡’ty G , Aâ€² , d) âŸ§ dir V Vâ€²)
  Goal
      with G â‰¡áµ G
  ... | yes refl = cong-Ã—áµ’ (â‰¡áµ’-refl refl) (cong-Ã—áµ’ (â‰¡áµ’-refl refl)
            (â‰¡áµ’-sym (fixpointáµ’ pre-â„°âŠğ’±
                        (injâ‚ ((gndâ‡’ty G , Aâ€² , d) , dir , V , Vâ€²)))))
  ... | no neq = âŠ¥-elim (neq refl)

ğ’±-dyn-any-elim-step : âˆ€{V}{Vâ€²}{dir}{k}{H}{Aâ€²}{c : gndâ‡’ty H âŠ‘ Aâ€²}
   â†’ #(ğ’±âŸ¦ â˜… , Aâ€² , unkâŠ‘ c âŸ§ dir V Vâ€²) (suc k)
   â†’ âˆƒ[ Vâ‚ ] V â‰¡ Vâ‚ âŸ¨ H !âŸ© Ã— Value Vâ‚ Ã— Value Vâ€²
             Ã— #(ğ’±âŸ¦ gndâ‡’ty H , Aâ€² , c âŸ§ dir Vâ‚ Vâ€²) (suc k)
ğ’±-dyn-any-elim-step {V âŸ¨ G !âŸ©}{Vâ€²}{dir}{k}{H}{Aâ€²}{c} ğ’±VGVâ€²
    with G â‰¡áµ H
... | no neq = âŠ¥-elim ğ’±VGVâ€²
... | yes refl
    with ğ’±VGVâ€²
... | v , vâ€² , ğ’±VVâ€² = V , refl , v , vâ€² , ğ’±VVâ€²

Value-inj-inv : âˆ€{V}{G}
   â†’ Value (V âŸ¨ G !âŸ©)
   â†’ Value V
Value-inj-inv {V} {G} (v âŒ© .G âŒª) = v

ğ’±-dyn-dyn : âˆ€{V}{Vâ€²}{G}{dir}
   â†’ ğ’±âŸ¦ â˜… , â˜… , unkâŠ‘unk âŸ§ dir (V âŸ¨ G !âŸ©) (Vâ€² âŸ¨ G !âŸ©)
     â‰¡áµ’ (Value V)áµ’ Ã—áµ’ (Value Vâ€²)áµ’
         Ã—áµ’ (â–·áµ’ (ğ’±âŸ¦ (gndâ‡’ty G , gndâ‡’ty G , ReflâŠ‘) âŸ§ dir V Vâ€²))
ğ’±-dyn-dyn {V}{Vâ€²}{G}{dir} =
  let X = injâ‚ ((â˜… , â˜… , unkâŠ‘unk) , dir , (V âŸ¨ G !âŸ©) , (Vâ€² âŸ¨ G !âŸ©)) in 
  ğ’±âŸ¦ â˜… , â˜… , unkâŠ‘unk âŸ§ dir (V âŸ¨ G !âŸ©) (Vâ€² âŸ¨ G !âŸ©)
    â©¦âŸ¨ â‰¡áµ’-refl refl âŸ©
  â„°âŠğ’± X
    â©¦âŸ¨ fixpointáµ’ pre-â„°âŠğ’± X  âŸ©
  # (pre-â„°âŠğ’± X) (â„°âŠğ’± , ttáµ–)
    â©¦âŸ¨ EQ âŸ©
   (Value V)áµ’ Ã—áµ’ (Value Vâ€²)áµ’
         Ã—áµ’ (â–·áµ’ (ğ’±âŸ¦ (gndâ‡’ty G , gndâ‡’ty G , ReflâŠ‘) âŸ§ dir V Vâ€²)) âˆ
  where
  X = injâ‚ ((â˜… , â˜… , unkâŠ‘unk) , dir , (V âŸ¨ G !âŸ©) , (Vâ€² âŸ¨ G !âŸ©))
  EQ : # (pre-â„°âŠğ’± X) (â„°âŠğ’± , ttáµ–)
    â‰¡áµ’ (Value V)áµ’ Ã—áµ’ (Value Vâ€²)áµ’
         Ã—áµ’ (â–·áµ’ (ğ’±âŸ¦ (gndâ‡’ty G , gndâ‡’ty G , ReflâŠ‘) âŸ§ dir V Vâ€²))
  EQ
      with G â‰¡áµ G
  ... | yes refl = â‰¡áµ’-refl refl
  ... | no neq = âŠ¥-elim (neq refl)

unkâŠ‘gnd-inv : âˆ€{G}
   â†’ (c : â˜… âŠ‘ gndâ‡’ty G)
   â†’ âˆƒ[ d ] c â‰¡ unkâŠ‘{G}{gndâ‡’ty G} d
unkâŠ‘gnd-inv {$áµ Î¹} (unkâŠ‘ {$áµ .Î¹} baseâŠ‘) = baseâŠ‘ , refl
unkâŠ‘gnd-inv {â˜…â‡’â˜…} (unkâŠ‘ {â˜…â‡’â˜…} (funâŠ‘ c d)) = funâŠ‘ c d , refl

ğ’±-dyn-R-step : âˆ€{G}{c : â˜… âŠ‘ gndâ‡’ty G}{V}{Vâ€²}{dir}{k}
   â†’ #(ğ’±âŸ¦ â˜… , gndâ‡’ty G , c âŸ§ dir V Vâ€²) k
   â†’ #(ğ’±âŸ¦ â˜… , â˜… , unkâŠ‘unk âŸ§ dir V (Vâ€² âŸ¨ G !âŸ©)) k
ğ’±-dyn-R-step {G} {c} {V} {Vâ€²} {dir} {zero} ğ’±VVâ€² =
     tz (ğ’±âŸ¦ â˜… , â˜… , unkâŠ‘unk âŸ§ dir V (Vâ€² âŸ¨ G !âŸ©))
ğ’±-dyn-R-step {G} {c} {V} {Vâ€²} {dir} {suc k} ğ’±VVâ€²
    with unkâŠ‘gnd-inv c
... | d , refl
    with ğ’±-dyn-any-elim-step {V}{Vâ€²}{dir}{k}{G}{_}{d} ğ’±VVâ€²
... | Vâ‚ , refl , vâ‚ , vâ€² , ğ’±Vâ‚Vâ€²
    with G â‰¡áµ G
... | no neq = âŠ¥-elim ğ’±VVâ€²
... | yes refl
    with gnd-prec-unique d ReflâŠ‘
... | refl =
    let ğ’±Vâ‚Vâ€²k = down (ğ’±âŸ¦ gndâ‡’ty G , gndâ‡’ty G , d âŸ§ dir Vâ‚ Vâ€²)
                       (suc k) ğ’±Vâ‚Vâ€² k (nâ‰¤1+n k) in
    vâ‚ , vâ€² , ğ’±Vâ‚Vâ€²k

-- ğ’±-dyn-R : âˆ€{ğ’«}{G}{c : â˜… âŠ‘ gndâ‡’ty G}{V}{Vâ€²}{dir}
--    â†’ ğ’« âŠ¢áµ’ ğ’±âŸ¦ â˜… , gndâ‡’ty G , c âŸ§ dir V Vâ€²
--    â†’ ğ’« âŠ¢áµ’ ğ’±âŸ¦ â˜… , â˜… , unkâŠ‘ âŸ§ dir V (Vâ€² âŸ¨ G !âŸ©)
-- ğ’±-dyn-R {ğ’«} {$áµ Î¹} {unkâŠ‘} {V} {Vâ€²}{dir} âŠ¢ğ’±VVâ€² =
--   ğ’±-dyn-base-elim âŠ¢ğ’±VVâ€² G
--   where
--   G : âˆ€{Vâ‚} {k} â†’ V â‰¡ (Vâ‚ âŸ¨ $áµ Î¹ !âŸ©) â†’ Vâ€² â‰¡ $ k
--      â†’ ğ’« âŠ¢áµ’ (Value Vâ‚)áµ’ Ã—áµ’ (â–·áµ’ ğ’±âŸ¦ $â‚œ Î¹ , $â‚œ Î¹ , baseâŠ‘ âŸ§ dir Vâ‚ ($ k))
--      â†’ ğ’« âŠ¢áµ’ ğ’±âŸ¦ â˜… , â˜… , unkâŠ‘ âŸ§ dir V (Vâ€² âŸ¨ $áµ Î¹ !âŸ©)
--   G {Vâ‚} {k} refl refl âŠ¢vâ‚Ã—ğ’±Vâ‚k =
--     substáµ’ (â‰¡áµ’-sym ğ’±-dyn-dyn)
--       (projâ‚áµ’ âŠ¢vâ‚Ã—ğ’±Vâ‚k ,áµ’ (constáµ’I ($Ì¬ k) ,áµ’ projâ‚‚áµ’ âŠ¢vâ‚Ã—ğ’±Vâ‚k))
-- ğ’±-dyn-R {ğ’«} {â˜…â‡’â˜…} {unkâŠ‘} {V} {Vâ€²}{dir} âŠ¢ğ’±VVâ€² = ğ’±-dyn-fun-elim âŠ¢ğ’±VVâ€² G
--   where
--   G : âˆ€ {Vâ‚} â†’ V â‰¡ (Vâ‚ âŸ¨ â˜…â‡’â˜… !âŸ©)
--      â†’ ğ’« âŠ¢áµ’ Value Vâ‚ áµ’ Ã—áµ’ Value Vâ€² áµ’
--           Ã—áµ’ (â–·áµ’ ğ’±âŸ¦ â˜… â‡’ â˜… , â˜… â‡’ â˜… , funâŠ‘ unkâŠ‘ unkâŠ‘ âŸ§ dir Vâ‚ Vâ€²)
--      â†’ ğ’« âŠ¢áµ’ ğ’±âŸ¦ â˜… , â˜… , unkâŠ‘ âŸ§ dir V (Vâ€² âŸ¨ â˜…â‡’â˜… !âŸ©)
--   G {Vâ‚} refl âŠ¢vÃ—vâ€²Ã—â–·ğ’±Vâ‚Vâ€² = substáµ’ (â‰¡áµ’-sym ğ’±-dyn-dyn) âŠ¢vÃ—vâ€²Ã—â–·ğ’±Vâ‚Vâ€²

-- ğ’±-dyn-dyn-elim : âˆ€{ğ’«}{V}{Vâ€²}{R}{dir}
--    â†’ ğ’« âŠ¢áµ’ ğ’±âŸ¦ â˜… , â˜… , unkâŠ‘ âŸ§ dir V Vâ€²
--    â†’ (âˆ€{Vâ‚}{Vâ‚â€²}{G} â†’ V â‰¡ Vâ‚ âŸ¨ G !âŸ© â†’ Vâ€² â‰¡ Vâ‚â€² âŸ¨ G !âŸ©
--          â†’ ğ’« âŠ¢áµ’ (Value Vâ‚)áµ’ Ã—áµ’ (Value Vâ‚â€²)áµ’
--              Ã—áµ’ â–·áµ’ ğ’±âŸ¦ gndâ‡’ty G , gndâ‡’ty G , ReflâŠ‘ âŸ§ dir Vâ‚ Vâ‚â€² â†’ ğ’« âŠ¢áµ’ R)
--    â†’ ğ’« âŠ¢áµ’ R
-- ğ’±-dyn-dyn-elim {ğ’«}{V}{Vâ€²}{R}{dir} âŠ¢ğ’±VVâ€² cont =
--   âŠ¢áµ’-sucP âŠ¢ğ’±VVâ€² Î» ğ’±VVâ€² â†’ G ğ’±VVâ€² âŠ¢ğ’±VVâ€² cont
--   where
--   G : âˆ€{V}{Vâ€²}{n}
--      â†’ # (ğ’±âŸ¦ â˜… , â˜… , unkâŠ‘ âŸ§ dir V Vâ€²) (suc n)
--      â†’ ğ’« âŠ¢áµ’ ğ’±âŸ¦ â˜… , â˜… , unkâŠ‘ âŸ§ dir V Vâ€²
--      â†’ (âˆ€{Vâ‚}{Vâ‚â€²}{G} â†’ V â‰¡ Vâ‚ âŸ¨ G !âŸ© â†’ Vâ€² â‰¡ Vâ‚â€² âŸ¨ G !âŸ©
--          â†’ ğ’« âŠ¢áµ’ (Value Vâ‚)áµ’ Ã—áµ’ (Value Vâ‚â€²)áµ’
--              Ã—áµ’ â–·áµ’ ğ’±âŸ¦ gndâ‡’ty G , gndâ‡’ty G , ReflâŠ‘ âŸ§ dir Vâ‚ Vâ‚â€² â†’ ğ’« âŠ¢áµ’ R)
--      â†’ ğ’« âŠ¢áµ’ R
--   G {Vâ‚ âŸ¨ G !âŸ©}{Vâ‚â€² âŸ¨ H !âŸ©}{n} ğ’±VVâ€² âŠ¢ğ’±VVâ€² cont
--       with G â‰¡áµ H
--   ... | no neq = âŠ¥-elim ğ’±VVâ€²
--   ... | yes refl = cont refl refl (substáµ’ ğ’±-dyn-dyn âŠ¢ğ’±VVâ€²)

ğ’±-dyn-L-step : âˆ€{G}{Aâ€²}{c : gndâ‡’ty G âŠ‘ Aâ€²}{V}{Vâ€²}{dir}{k}
   â†’ #(ğ’±âŸ¦ gndâ‡’ty G , Aâ€² , c âŸ§ dir V Vâ€²) k
   â†’ #(ğ’±âŸ¦ â˜… , Aâ€² , unkâŠ‘ c âŸ§ dir (V âŸ¨ G !âŸ©) Vâ€²) k
ğ’±-dyn-L-step {G}{Aâ€²}{c}{V}{Vâ€²}{dir}{zero} ğ’±VVâ€²k =
    tz (ğ’±âŸ¦ â˜… , Aâ€² , unkâŠ‘ c âŸ§ dir (V âŸ¨ G !âŸ©) Vâ€²)
ğ’±-dyn-L-step {G} {Aâ€²} {c} {V} {Vâ€²} {dir} {suc k} ğ’±VVâ€²k
    with G â‰¡áµ G
... | no neq = âŠ¥-elim (neq refl)
... | yes refl =
      let (v , vâ€²) = ğ’±â‡’Value (gndâ‡’ty G , Aâ€² , c) V Vâ€² ğ’±VVâ€²k in
      v , vâ€² , ğ’±VVâ€²k

-- ğ’±-dyn-L : âˆ€{ğ’«}{G}{Aâ€²}{c : gndâ‡’ty G âŠ‘ Aâ€²}{V}{Vâ€²}{dir}
--    â†’ ğ’« âŠ¢áµ’ ğ’±âŸ¦ gndâ‡’ty G , Aâ€² , c âŸ§ dir V Vâ€²
--    â†’ ğ’« âŠ¢áµ’ ğ’±âŸ¦ â˜… , Aâ€² , unkâŠ‘ âŸ§ dir (V âŸ¨ G !âŸ©) Vâ€²
-- ğ’±-dyn-L {ğ’«} {$áµ Î¹} {$â‚œ Î¹â€²} {c} {V} {Vâ€²}{dir} ğ’±VVâ€² =
--   ğ’±-base-elim ğ’±VVâ€² Î» {k refl refl refl â†’ G}
--   where
--   G : âˆ€{k} â†’ ğ’« âŠ¢áµ’ ğ’±âŸ¦ â˜… , $â‚œ Î¹ , unkâŠ‘ âŸ§ dir ($ k âŸ¨ $áµ Î¹ !âŸ©) ($ k)
--   G {k} = substáµ’ (â‰¡áµ’-sym ğ’±-dyn-base) (constáµ’I ($Ì¬ k) ,áµ’ monoáµ’ ğ’±-base-intro)
-- ğ’±-dyn-L {ğ’«} {â˜…â‡’â˜…} {Aâ€² â‡’ Bâ€²} {funâŠ‘ unkâŠ‘ unkâŠ‘} {V} {Vâ€²} âŠ¢ğ’±VVâ€² =
--   âŠ¢áµ’-sucP âŠ¢ğ’±VVâ€² Î» ğ’±VVâ€² â†’
--   let (v , vâ€²) = ğ’±â‡’Value (â˜… â‡’ â˜… , Aâ€² â‡’ Bâ€² , funâŠ‘ unkâŠ‘ unkâŠ‘) V Vâ€² ğ’±VVâ€² in
--   substáµ’ (â‰¡áµ’-sym ğ’±-dyn-fun) (constáµ’I v ,áµ’ (constáµ’I vâ€² ,áµ’ monoáµ’ âŠ¢ğ’±VVâ€²))

{--------------- Related values are related expressions -----------------------}

ğ’±â‡’â„°-step : âˆ€{c : Prec}{V Vâ€²}{dir}{k}
   â†’ #(ğ’±âŸ¦ c âŸ§ dir V Vâ€²) k
     ---------------------
   â†’ #(â„°âŸ¦ c âŸ§ dir V Vâ€²) k
ğ’±â‡’â„°-step {c} {V} {Vâ€²} {dir} {zero} ğ’±VVâ€²k = tz (â„°âŸ¦ c âŸ§ dir V Vâ€²)
ğ’±â‡’â„°-step {c} {V} {Vâ€²} {â‰º} {suc k} ğ’±VVâ€²sk =
  â‡”-fro (â„°-suc{c}{â‰º})
  (let (v , vâ€²) = ğ’±â‡’Value c V Vâ€² ğ’±VVâ€²sk in
  (injâ‚‚ (injâ‚‚ (v , injâ‚‚ (Vâ€² , (Vâ€² END) , vâ€² , ğ’±VVâ€²sk)))))
ğ’±â‡’â„°-step {c} {V} {Vâ€²} {â‰»} {suc k} ğ’±VVâ€²sk =
  â‡”-fro (â„°-suc{c}{â‰»})
  (let (v , vâ€²) = ğ’±â‡’Value c V Vâ€² ğ’±VVâ€²sk in
  injâ‚‚ (injâ‚‚ (vâ€² , V , (V END) , v , ğ’±VVâ€²sk)))

ğ’±â‡’â„° : âˆ€{c : Prec}{ğ’«}{V Vâ€²}{dir}
   â†’ ğ’« âŠ¢áµ’ ğ’±âŸ¦ c âŸ§ dir V Vâ€²
     ---------------------
   â†’ ğ’« âŠ¢áµ’ â„°âŸ¦ c âŸ§ dir V Vâ€²
ğ’±â‡’â„° {c}{ğ’«}{V}{Vâ€²}{â‰º} âŠ¢ğ’±VVâ€² = substáµ’ (â‰¡áµ’-sym â„°-stmt)
  (âŠ¢áµ’-sucP âŠ¢ğ’±VVâ€² Î» ğ’±VVâ€² â†’
  let (v , vâ€²) = ğ’±â‡’Value c V Vâ€² ğ’±VVâ€² in
  (injâ‚‚áµ’ (injâ‚‚áµ’ (constáµ’I v ,áµ’
   injâ‚‚áµ’ (âŠ¢áµ’-âˆƒ-intro-new (Î» X â†’ (Vâ€² â€”â†  X)áµ’ Ã—áµ’ (Value X)áµ’ Ã—áµ’ (ğ’±âŸ¦ c âŸ§ â‰º V X))
            Vâ€² (constáµ’I (Vâ€² END) ,áµ’
            (constáµ’I vâ€² ,áµ’ âŠ¢ğ’±VVâ€²)))))))
ğ’±â‡’â„° {c}{ğ’«}{V}{Vâ€²}{â‰»} âŠ¢ğ’±VVâ€² = substáµ’ (â‰¡áµ’-sym â„°-stmt)
  (âŠ¢áµ’-sucP âŠ¢ğ’±VVâ€² Î» ğ’±VVâ€² â†’
  let (v , vâ€²) = ğ’±â‡’Value c V Vâ€² ğ’±VVâ€² in
  (injâ‚‚áµ’ (injâ‚‚áµ’ (constáµ’I vâ€² ,áµ’
   (âŠ¢áµ’-âˆƒ-intro-new (Î» X â†’ (V â€”â†  X)áµ’ Ã—áµ’ (Value X)áµ’ Ã—áµ’ (ğ’±âŸ¦ c âŸ§ â‰» X Vâ€²))
            V (constáµ’I (V END) ,áµ’
            (constáµ’I v ,áµ’ âŠ¢ğ’±VVâ€²)))))))

{--------------- Blame on the right -------------------------------------------}

â„°-blame : âˆ€{ğ’«}{c}{M}{dir} â†’ ğ’« âŠ¢áµ’ â„°âŸ¦ c âŸ§ dir M blame
â„°-blame {ğ’«}{c}{M}{dir} = âŠ¢áµ’-intro Î» n x â†’ â„°-blame-step{c}{dir}
